// BSLLS:LineLength-off
// BSLLS:MagicNumber-off
// BSLLS:DuplicateStringLiteral-off

#Использовать "helpers"

Перем Поделка; // Поделка
Перем ТекущийКаталог; // Строка

&Инициализация
Процедура Инициализация() Экспорт

	Поделка = Новый Поделка();
	Поделка.ЗапуститьПриложение();
	
	ТекущийКаталог = ТекущийКаталог();

КонецПроцедуры

&После
Процедура ПослеКаждого() Экспорт
	УстановитьТекущийКаталог(ТекущийКаталог);
КонецПроцедуры

&Тест
Процедура ТестДолжен_ОпределитьМанифестВТекущемКаталоге() Экспорт

	// Подготовка
	ЛокаторМанифеста = Поделка.НайтиЖелудь("ЛокаторМанифеста");

	УстановитьТекущийКаталог("./tests/fixtures");

	// Действие
	ПутьКФайлу = ЛокаторМанифеста.Определить();

	// Проверка
	Файл = Новый Файл(ПутьКФайлу);

	Ожидаем.Что(Файл.Существует()).ЭтоИстина();
	Ожидаем.Что(Файл.Имя).Равно("packagedef");

КонецПроцедуры

&Тест
Процедура ТестДолжен_ОпределитьМанифестВУказанномКаталоге() Экспорт

	// Подготовка
	ЛокаторМанифеста = Поделка.НайтиЖелудь("ЛокаторМанифеста");

	// Действие
	ПутьКФайлу = ЛокаторМанифеста.Определить("./tests/fixtures");

	// Проверка
	Файл = Новый Файл(ПутьКФайлу);

	Ожидаем.Что(Файл.Существует()).ЭтоИстина();
	Ожидаем.Что(Файл.Имя).Равно("packagedef");

КонецПроцедуры

&Тест
Процедура ТестДолжен_ВернутьТотЖеФайлПриОпределении() Экспорт

	// Подготовка
	ЛокаторМанифеста = Поделка.НайтиЖелудь("ЛокаторМанифеста");

	// Действие
	ПутьКФайлу = ЛокаторМанифеста.Определить(ПомощникТестирования.ФайлМанифеста);

	// Проверка
	Ожидаем.Что(ПутьКФайлу).Равно(ПомощникТестирования.ФайлМанифеста);

КонецПроцедуры

&Тест
Процедура Тест_ПроверяетОшибкуОтсутствияМанифестаВТекущемКаталоге() Экспорт

	// Подготовка
	ЛокаторМанифеста = Поделка.НайтиЖелудь("ЛокаторМанифеста");

	УстановитьТекущийКаталог("./tests");

	// Действие и Проверка
	Ожидаем
		.Что(ЛокаторМанифеста)
		.Метод("Определить")
		.ВыбрасываетИсключение("Не найден файл манифеста в текущем каталоге");

КонецПроцедуры

&Тест
Процедура Тест_ПроверяетОшибкуОтсутствияМанифестаВУказанномКаталоге() Экспорт

	// Подготовка
	ЛокаторМанифеста = Поделка.НайтиЖелудь("ЛокаторМанифеста");

	ПараметрыМетода = Новый Массив();
	ПараметрыМетода.Добавить("./tests");

	// Действие и Проверка
	Ожидаем
		.Что(ЛокаторМанифеста)
		.Метод("Определить", ПараметрыМетода)
		.ВыбрасываетИсключение("Не найден файл манифеста в каталоге: ./tests");

КонецПроцедуры

&Тест
Процедура Тест_ПроверяетОшибкуНесуществующегоКаталога() Экспорт

	// Подготовка
	ЛокаторМанифеста = Поделка.НайтиЖелудь("ЛокаторМанифеста");

	ПараметрыМетода = Новый Массив();
	ПараметрыМетода.Добавить("./testsss");

	// Действие и Проверка
	Ожидаем
		.Что(ЛокаторМанифеста)
		.Метод("Определить", ПараметрыМетода)
		.ВыбрасываетИсключение("Каталог или файл не существует: ./testsss");

КонецПроцедуры