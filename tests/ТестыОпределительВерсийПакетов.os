// BSLLS:LineLength-off
// BSLLS:MagicNumber-off
// BSLLS:DuplicateStringLiteral-off

#Использовать "helpers"

Перем Поделка; // Поделка

&Инициализация
Процедура Инициализация() Экспорт

	Поделка = Новый Поделка();
	Поделка.ЗапуститьПриложение();

КонецПроцедуры

&Тест
Процедура ТестДолжен_НайтиПоследнююВерсиюПакетаСтрокой() Экспорт

	// Подготовка
	Определитель = Поделка.НайтиЖелудь("ОпределительВерсийПакетов");

	// Действие
	ВерсииПакетов = Определитель.НайтиВерсии("autumn");

	// Проверка
	Ожидаем.Что(ВерсииПакетов["autumn"]).Равно("4.3.11");

КонецПроцедуры

&Тест
Процедура ТестДолжен_НайтиПоследниеВерсииПакетовСтрокой() Экспорт

	// Подготовка
	Определитель = Поделка.НайтиЖелудь("ОпределительВерсийПакетов");

	// Действие
	ВерсииПакетов = Определитель.НайтиВерсии("autumn, asserts");

	// Проверка
	Ожидаем.Что(ВерсииПакетов["autumn"], "autumn").Равно("4.3.11");
	Ожидаем.Что(ВерсииПакетов["asserts"], "asserts").Равно("1.5.0");

КонецПроцедуры

&Тест
Процедура ТестДолжен_НайтиПоследниеВерсииПакетовМассивом() Экспорт

	// Подготовка
	Определитель = Поделка.НайтиЖелудь("ОпределительВерсийПакетов");

	// Действие
	ВерсииПакетов = Определитель.НайтиВерсии(СтрРазделить("autumn,asserts", ","));

	// Проверка
	Ожидаем.Что(ВерсииПакетов["autumn"], "autumn").Равно("4.3.11");
	Ожидаем.Что(ВерсииПакетов["asserts"], "asserts").Равно("1.5.0");

КонецПроцедуры

&Тест
Процедура ТестДолжен_НайтиПоследниеВерсииПакетовТаблицей() Экспорт

	// Подготовка
	Таблица = Новый ТаблицаЗначений();
	Таблица.Колонки.Добавить("ИмяПакета");
	Таблица.Колонки.Добавить("МинимальнаяВерсия");

	СтрокаТаблицы = Таблица.Добавить();
	СтрокаТаблицы.ИмяПакета = "autumn";

	СтрокаТаблицы = Таблица.Добавить();
	СтрокаТаблицы.ИмяПакета = "asserts";

	Определитель = Поделка.НайтиЖелудь("ОпределительВерсийПакетов");

	// Действие
	ВерсииПакетов = Определитель.НайтиВерсии(Таблица);

	// Проверка
	Ожидаем.Что(ВерсииПакетов["autumn"], "autumn").Равно("4.3.11");
	Ожидаем.Что(ВерсииПакетов["asserts"], "asserts").Равно("1.5.0");

КонецПроцедуры

&Тест
Процедура ТестДолжен_ПроверитьВыбрасываниеИсключенияНаНеправильныйТипПакета() Экспорт
		
	// Подготовка
	Определитель = Поделка.НайтиЖелудь("ОпределительВерсийПакетов");
	
	Параметры = Новый Массив();
	Параметры.Добавить(1);
	
	// Действие и проверка
	Ожидаем.Что(Определитель)
		.Метод("НайтиВерсии", Параметры)
		.ВыбрасываетИсключение("Неподдерживаемый тип параметра Пакеты: Число. Ожидался Строка, Массив или ТаблицаЗначений.");

КонецПроцедуры

&Тест
Процедура ТестДолжен_ПроверитьПередачуМинимальнойВерсииВСтроке() Экспорт

	// Подготовка
	Определитель = Поделка.НайтиЖелудь("ОпределительВерсийПакетов");

	// Действие
	ВерсииПакетов = Определитель.НайтиВерсии("autumn@3.2.0", ТипыВерсий.Минорная);

	// Проверка
	Ожидаем.Что(ВерсииПакетов["autumn"]).Равно("3.3.0");

КонецПроцедуры

&Тест
Процедура ТестДолжен_ПроверитьПередачуМинимальнойВерсииВМассиве() Экспорт

	// Подготовка
	Определитель = Поделка.НайтиЖелудь("ОпределительВерсийПакетов");

	// Действие
	ВерсииПакетов = Определитель.НайтиВерсии(СтрРазделить("autumn@3.2.0", ","), ТипыВерсий.Минорная);

	// Проверка
	Ожидаем.Что(ВерсииПакетов["autumn"]).Равно("3.3.0");

КонецПроцедуры

&Тест
Процедура ТестДолжен_ПроверитьПередачуМинимальнойВерсииВТаблице() Экспорт

	// Подготовка
	Таблица = Новый ТаблицаЗначений();
	Таблица.Колонки.Добавить("ИмяПакета");
	Таблица.Колонки.Добавить("МинимальнаяВерсия");

	СтрокаТаблицы = Таблица.Добавить();
	СтрокаТаблицы.ИмяПакета = "autumn";
	СтрокаТаблицы.МинимальнаяВерсия = "3.2.0";

	Определитель = Поделка.НайтиЖелудь("ОпределительВерсийПакетов");

	// Действие
	ВерсииПакетов = Определитель.НайтиВерсии(Таблица, ТипыВерсий.Минорная);

	// Проверка
	Ожидаем.Что(ВерсииПакетов["autumn"], "autumn").Равно("3.3.0");

КонецПроцедуры

&Тест
Процедура ТестДолжен_ПроверитьПолучениеМинорныхВерсий() Экспорт

	// Подготовка
	ТестовыеСлучаи = Новый Соответствие();
	ТестовыеСлучаи.Вставить("autumn", "4.3.11");
	ТестовыеСлучаи.Вставить("autumn@3.2.0", "3.3.0");
	ТестовыеСлучаи.Вставить("autumn@3.3.0", "3.3.0");
	ТестовыеСлучаи.Вставить("1testrunner@1.9.0", "1.9.2");

	Определитель = Поделка.НайтиЖелудь("ОпределительВерсийПакетов");

	Для Каждого ТестовыйСлучай Из ТестовыеСлучаи Цикл
		ИмяПакета = СтрРазделить(ТестовыйСлучай.Ключ, "@")[0];

		// Действие
		ВерсииПакетов = Определитель.НайтиВерсии(ТестовыйСлучай.Ключ, ТипыВерсий.Минорная);

		// Проверка
		Ожидаем.Что(ВерсииПакетов[ИмяПакета], ТестовыйСлучай.Ключ).Равно(ТестовыйСлучай.Значение);
	КонецЦикла;

КонецПроцедуры