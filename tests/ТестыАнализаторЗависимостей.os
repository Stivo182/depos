// BSLLS:LineLength-off
// BSLLS:MagicNumber-off
// BSLLS:DuplicateStringLiteral-off

#Использовать "helpers"

Перем Поделка; // Поделка

&Инициализация
Процедура Инициализация() Экспорт

	Поделка = Новый Поделка();
	Поделка.ЗапуститьПриложение();

КонецПроцедуры

&Тест
Процедура ТестДолжен_НайтиОтсутствующиеПакетыИзФайла() Экспорт
	
	// Подготовка
	ТестовыеСлучаи = ПустаяТаблицаТестовыхСлучаев();
	ДобавитьТестовыйСлучай(ТестовыеСлучаи, "present_in_dev_src_tests", Ложь, 1);
	ДобавитьТестовыйСлучай(ТестовыеСлучаи, "present_in_dev_src_missing_tests", Ложь, 1);
	ДобавитьТестовыйСлучай(ТестовыеСлучаи, "missing_manifests_present_src_tests", Ложь, 2);
	ДобавитьТестовыйСлучай(ТестовыеСлучаи, "missing_manifests_present_src_tests", Истина, 1);
	ДобавитьТестовыйСлучай(ТестовыеСлучаи, "missing_manifests_present_src", Ложь, 1);
	ДобавитьТестовыйСлучай(ТестовыеСлучаи, "missing_manifests_present_tests", Истина, 1);

	Анализатор = ПолучитьАнализаторПрочитанныйИзФайла();
	
	// Действие
	Результат = Анализатор.НайтиОшибки();
	Пакеты = Результат.ОтсутствующиеПакеты;

	// Проверка
	Ожидаем.Что(Пакеты, "Количество отсутствующих пакетов").ИмеетДлину(ТестовыеСлучаи.Количество());

	Для Каждого ТестовыйСлучай Из ТестовыеСлучаи Цикл

		Представление = СтрШаблон("Имя=%1, ДляРазработки=%2", 
			ТестовыйСлучай.ИмяПакета, ТестовыйСлучай.ДляРазработки);

		Отбор = Новый Структура("ИмяПакета, ДляРазработки", ТестовыйСлучай.ИмяПакета, ТестовыйСлучай.ДляРазработки);
		НайденныеСтроки = Пакеты.НайтиСтроки(Отбор);

		Ожидаем.Что(НайденныеСтроки, "Должен быть найден 1 отсутствующий пакет: " + Представление).ИмеетДлину(1);
		Ожидаем.Что(НайденныеСтроки[0].Файлы, "Количество файлов для " + Представление).ИмеетДлину(ТестовыйСлучай.КоличествоФайлов);

	КонецЦикла;

КонецПроцедуры

&Тест
Процедура ТестДолжен_НайтиНеиспользуемыеПакетыИзФайла() Экспорт
	
	// Подготовка
	ТестовыеСлучаи = ПустаяТаблицаТестовыхСлучаев();
	ДобавитьТестовыйСлучай(ТестовыеСлучаи, "present_in_prod_tests_missing_src", Ложь);
	ДобавитьТестовыйСлучай(ТестовыеСлучаи, "present_in_prod_missing_src_tests", Ложь);
	ДобавитьТестовыйСлучай(ТестовыеСлучаи, "present_in_dev_src_missing_tests", Истина);
	ДобавитьТестовыйСлучай(ТестовыеСлучаи, "present_in_dev_missing_src_tests", Истина);

	Анализатор = ПолучитьАнализаторПрочитанныйИзФайла();
	
	// Действие
	Результат = Анализатор.НайтиОшибки();
	Пакеты = Результат.НеиспользуемыеПакеты;

	// Проверка
	Ожидаем.Что(Пакеты, "Количество неиспользуемых пакетов").ИмеетДлину(ТестовыеСлучаи.Количество());

	Для Каждого ТестовыйСлучай Из ТестовыеСлучаи Цикл

		Представление = СтрШаблон(
			"Имя=%1, ДляРазработки=%2", 
			ТестовыйСлучай.ИмяПакета,
			ТестовыйСлучай.ДляРазработки
		);

		Отбор = Новый Структура("ИмяПакета, ДляРазработки", ТестовыйСлучай.ИмяПакета, ТестовыйСлучай.ДляРазработки);
		НайденныеСтроки = Пакеты.НайтиСтроки(Отбор);

		Ожидаем.Что(НайденныеСтроки, "Должен быть найден 1 неиспользуемый пакет: " + Представление).ИмеетДлину(1);

	КонецЦикла;

КонецПроцедуры

&Тест
Процедура ТестДолжен_НайтиНеиспользуемыеПакетыИзТаблицы() Экспорт
	
	// Подготовка
	ТестовыеСлучаи = ПустаяТаблицаТестовыхСлучаев();
	ДобавитьТестовыйСлучай(ТестовыеСлучаи, "present_in_prod_tests_missing_src", Ложь);
	ДобавитьТестовыйСлучай(ТестовыеСлучаи, "present_in_prod_missing_src_tests", Ложь);
	ДобавитьТестовыйСлучай(ТестовыеСлучаи, "present_in_dev_src_missing_tests", Истина);
	ДобавитьТестовыйСлучай(ТестовыеСлучаи, "present_in_dev_missing_src_tests", Истина);

	Анализатор = ПолучитьАнализаторПрочитанныйИзТаблицы();
	
	// Действие
	Результат = Анализатор.НайтиОшибки();
	Пакеты = Результат.НеиспользуемыеПакеты;

	// Проверка
	Ожидаем.Что(Пакеты, "Количество неиспользуемых пакетов").ИмеетДлину(ТестовыеСлучаи.Количество());

	Для Каждого ТестовыйСлучай Из ТестовыеСлучаи Цикл

		Представление = СтрШаблон(
			"Имя=%1, ДляРазработки=%2", 
			ТестовыйСлучай.ИмяПакета,
			ТестовыйСлучай.ДляРазработки
		);

		Отбор = Новый Структура("ИмяПакета, ДляРазработки", ТестовыйСлучай.ИмяПакета, ТестовыйСлучай.ДляРазработки);
		НайденныеСтроки = Пакеты.НайтиСтроки(Отбор);

		Ожидаем.Что(НайденныеСтроки, "Должен быть найден 1 неиспользуемый пакет: " + Представление).ИмеетДлину(1);

	КонецЦикла;

КонецПроцедуры

&Тест
Процедура ТестДолжен_ПроверитьИсключениеПакетовДляРазработки() Экспорт
	
	// Подготовка
	ТестовыеСлучаи = ПустаяТаблицаТестовыхСлучаев();
	ДобавитьТестовыйСлучай(ТестовыеСлучаи, "present_in_prod_tests_missing_src", Ложь);
	ДобавитьТестовыйСлучай(ТестовыеСлучаи, "present_in_prod_missing_src_tests", Ложь);

	Анализатор = ПолучитьАнализаторПрочитанныйИзФайла();
	Анализатор.ИсключитьЗависимостиДляРазработки();
	
	// Действие
	Результат = Анализатор.НайтиОшибки();
	Пакеты = Результат.НеиспользуемыеПакеты;

	// Проверка
	Ожидаем.Что(Пакеты, "Количество неиспользуемых пакетов").ИмеетДлину(ТестовыеСлучаи.Количество());

	Для Каждого ТестовыйСлучай Из ТестовыеСлучаи Цикл

		Представление = СтрШаблон(
			"Имя=%1, ДляРазработки=%2", 
			ТестовыйСлучай.ИмяПакета,
			ТестовыйСлучай.ДляРазработки
		);

		Отбор = Новый Структура("ИмяПакета, ДляРазработки", ТестовыйСлучай.ИмяПакета, ТестовыйСлучай.ДляРазработки);
		НайденныеСтроки = Пакеты.НайтиСтроки(Отбор);

		Ожидаем.Что(НайденныеСтроки, "Должен быть найден 1 неиспользуемый пакет: " + Представление).ИмеетДлину(1);

	КонецЦикла;

КонецПроцедуры

&Тест
Процедура ТестДолжен_ПроверитьВыбрасываниеИсключенияКогдаНеУказанМанифест() Экспорт

	// Подготовка
	АнализаторЗависимостей = Поделка.НайтиЖелудь("АнализаторЗависимостей");

	// Действие и проверка
	Ожидаем
		.Что(АнализаторЗависимостей)
		.Метод("НайтиОшибки")
		.ВыбрасываетИсключение("Не указан источник зависимостей (файл манифеста или таблица).");

КонецПроцедуры

Функция ПолучитьАнализаторПрочитанныйИзФайла()

	Возврат Поделка.НайтиЖелудь("АнализаторЗависимостей")
		.ИзФайла("./tests/fixtures/ПроектДляАнализа/packagedef")
		.УстановитьКаталогиИсходников("src")
		.УстановитьВспомогательныеКаталогиИсходников("tests");

КонецФункции

Функция ПолучитьАнализаторПрочитанныйИзТаблицы()

	ИнформацияОПакете = Новый ИнформацияОПакете("./tests/fixtures/ПроектДляАнализа/packagedef");
	
	Возврат Поделка.НайтиЖелудь("АнализаторЗависимостей")
		.ИзТаблицы(ИнформацияОПакете.Зависимости())
		.УстановитьКаталогиИсходников("./tests/fixtures/ПроектДляАнализа/src")
		.УстановитьВспомогательныеКаталогиИсходников("./tests/fixtures/ПроектДляАнализа/tests");

КонецФункции

Функция ПустаяТаблицаТестовыхСлучаев()

	Таблица = Новый ТаблицаЗначений();
	Таблица.Колонки.Добавить("ИмяПакета");
	Таблица.Колонки.Добавить("ДляРазработки");
	Таблица.Колонки.Добавить("КоличествоФайлов");

	Возврат Таблица;

КонецФункции

Процедура ДобавитьТестовыйСлучай(Таблица, ИмяПакета, ДляРазработки, КоличествоФайлов = Неопределено)

	СтрокаТаблицы = Таблица.Добавить();
	СтрокаТаблицы.ИмяПакета = ИмяПакета;
	СтрокаТаблицы.ДляРазработки = ДляРазработки;
	СтрокаТаблицы.КоличествоФайлов = КоличествоФайлов;

КонецПроцедуры