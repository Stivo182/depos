// BSLLS:LineLength-off
// BSLLS:MagicNumber-off
// BSLLS:DuplicateStringLiteral-off
#Использовать "helpers"

Перем Поделка; // Поделка

&Инициализация
Процедура Инициализация() Экспорт

	Поделка = Новый Поделка();
	Поделка.ЗапуститьПриложение();

КонецПроцедуры

&После
Процедура ПослеКаждого() Экспорт
	ПомощникТестирования.УдалитьВременныеФайлы();
КонецПроцедуры

&Тест
Процедура ТестДолжен_ПроверитьБазовыйСлучайИзФайла() Экспорт

	// Подготовка
	Определитель = Поделка.НайтиЖелудь("ОпределительВерсийЗависимостей");
	Зависимости = Новый ЗависимостиПакета().ПрочитатьИзМанифеста(ПомощникТестирования.ФайлМанифеста);

	// Действие
	Результат = Определитель.НайтиВерсии(Зависимости).ВТаблицу();

	// Проверка
	Ожидаем.Что(Результат).ИмеетДлину(6);
	Ожидаем.Что(Результат[0].ИмяПакета).Равно("delegate");
	Ожидаем.Что(Результат[0].МинимальнаяВерсия).Равно("1.0.0");
	Ожидаем.Что(Результат[1].ИмяПакета).Равно("annotations");
	Ожидаем.Что(Результат[1].МинимальнаяВерсия).Равно("1.3.1");
	Ожидаем.Что(Результат[2].ИмяПакета).Равно("asserts");
	Ожидаем.Что(Результат[2].МинимальнаяВерсия).Равно("1.5.0");
	Ожидаем.Что(Результат[3].ИмяПакета).Равно("packageinfo");
	Ожидаем.Что(Результат[3].МинимальнаяВерсия).Равно("0.8");
	Ожидаем.Что(Результат[4].ИмяПакета).Равно("autumn");
	Ожидаем.Что(Результат[4].МинимальнаяВерсия).Равно("4.3.11");
	Ожидаем.Что(Результат[5].ИмяПакета).Равно("1testrunner");
	Ожидаем.Что(Результат[5].МинимальнаяВерсия).Равно("1.9.2");

КонецПроцедуры

&Тест
Процедура ТестДолжен_ПроверитьНаличиеТолькоУстаревшихПакетов() Экспорт

	// Подготовка
	Определитель = Поделка.НайтиЖелудь("ОпределительВерсийЗависимостей");
	Зависимости = Новый ЗависимостиПакета().ПрочитатьИзМанифеста(ПомощникТестирования.ФайлМанифеста);

	// Действие
	Результат = Определитель
		.ТолькоУстаревшие()
		.НайтиВерсии(Зависимости)
		.ВТаблицу();

	// Проверка
	Ожидаем.Что(Результат).ИмеетДлину(4);
	Ожидаем.Что(Результат[0].ИмяПакета).Равно("annotations");
	Ожидаем.Что(Результат[0].МинимальнаяВерсия).Равно("1.3.1");
	Ожидаем.Что(Результат[1].ИмяПакета).Равно("packageinfo");
	Ожидаем.Что(Результат[1].МинимальнаяВерсия).Равно("0.8");
	Ожидаем.Что(Результат[2].ИмяПакета).Равно("autumn");
	Ожидаем.Что(Результат[2].МинимальнаяВерсия).Равно("4.3.11");
	Ожидаем.Что(Результат[3].ИмяПакета).Равно("1testrunner");
	Ожидаем.Что(Результат[3].МинимальнаяВерсия).Равно("1.9.2");

КонецПроцедуры

&Тест
Процедура ТестДолжен_ПроверитьТипВерсии() Экспорт

	// Подготовка
	ТестовыеСлучаи = Новый Соответствие();
	ТестовыеСлучаи.Вставить(
		ТипыВерсий.Последняя, 
		"annotations@1.3.1,asserts@1.5.0,autumn@4.3.11,delegate@1.0.0,packageinfo@0.8,1testrunner@1.9.2"
	);
	ТестовыеСлучаи.Вставить(
		ТипыВерсий.Минорная,
		"annotations@1.3.1,asserts@1.5.0,autumn@3.3.0,delegate@1.0.0,packageinfo@0.8,1testrunner@1.9.2"
	);
	ТестовыеСлучаи.Вставить(
		ТипыВерсий.Патч,
		"annotations@1.3.1,asserts@1.5.0,autumn@3.2.0,delegate@1.0.0,packageinfo@0.7,1testrunner@1.8.0"
	);

	Определитель = Поделка.НайтиЖелудь("ОпределительВерсийЗависимостей");
	Зависимости = Новый ЗависимостиПакета().ПрочитатьИзМанифеста(ПомощникТестирования.ФайлМанифеста);

	Для Каждого ТестовыйСлучай Из ТестовыеСлучаи Цикл

		ТипВерсии = ТестовыйСлучай.Ключ;
		ИменаПакетов = СтрРазделить(ТестовыйСлучай.Значение, ",", Ложь);

		// Действие
		Результат = Определитель.НайтиВерсии(Зависимости, ТипВерсии).ВТаблицу();

		// Проверка
		Ожидаем.Что(Результат, ТипВерсии).ИмеетДлину(ИменаПакетов.Количество());

		Для Каждого СтрокаРезультата Из Результат Цикл		
			ИмяИВерсия = СтрШаблон("%1@%2", СтрокаРезультата.ИмяПакета, СтрокаРезультата.МинимальнаяВерсия);
			Ожидаем.Что(ИменаПакетов, ТипВерсии).Содержит(ИмяИВерсия);
		КонецЦикла;

	КонецЦикла;

КонецПроцедуры