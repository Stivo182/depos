#Использовать autumn
#Использовать asserts
#Использовать packageinfo
#Использовать "../src/core"
#Использовать "helpers"
#Использовать "mocks"

Перем Поделка;

&Инициализация
Процедура Инициализация() Экспорт

	Поделка = Новый Поделка();
	Поделка.ЗапуститьПриложение();

КонецПроцедуры

&После
Процедура ПослеКаждого() Экспорт
	ПомощникТестирования.УдалитьВременныеФайлы();
КонецПроцедуры

&Тест
Процедура ТестДолжен_ПроверитьБазовыйСлучайИзФайла() Экспорт

	// Подготовка
	Определитель = Поделка.НайтиЖелудь("ОпределительВерсийЗависимостей");

	// Действие
	Результат = Определитель.ИзФайла(ПомощникТестирования.ФайлМанифеста).Получить();

	// Проверка
	Ожидаем.Что(Результат).ИмеетДлину(6);
	Ожидаем.Что(Результат[0].ИмяПакета).Равно("annotations");
	Ожидаем.Что(Результат[0].ЦелеваяВерсия).Равно("1.3.1");
	Ожидаем.Что(Результат[1].ИмяПакета).Равно("asserts");
	Ожидаем.Что(Результат[1].ЦелеваяВерсия).Равно("1.5.0");
	Ожидаем.Что(Результат[2].ИмяПакета).Равно("autumn");
	Ожидаем.Что(Результат[2].ЦелеваяВерсия).Равно("4.3.11");
	Ожидаем.Что(Результат[3].ИмяПакета).Равно("delegate");
	Ожидаем.Что(Результат[3].ЦелеваяВерсия).Равно("1.0.0");
	Ожидаем.Что(Результат[4].ИмяПакета).Равно("packageinfo");
	Ожидаем.Что(Результат[4].ЦелеваяВерсия).Равно("0.8");
	Ожидаем.Что(Результат[5].ИмяПакета).Равно("1testrunner");
	Ожидаем.Что(Результат[5].ЦелеваяВерсия).Равно("1.9.2");

КонецПроцедуры

&Тест
Процедура ТестДолжен_ПроверитьБазовыйСлучайИзТаблицы() Экспорт

	// Подготовка
	Определитель = Поделка.НайтиЖелудь("ОпределительВерсийЗависимостей");

	Зависимости = Новый ИнформацияОПакете(ПомощникТестирования.ФайлМанифеста).Зависимости();

	// Действие
	Результат = Определитель.ИзТаблицы(Зависимости).Получить();

	// Проверка
	Ожидаем.Что(Результат).ИмеетДлину(6);
	Ожидаем.Что(Результат[0].ИмяПакета).Равно("annotations");
	Ожидаем.Что(Результат[0].ЦелеваяВерсия).Равно("1.3.1");
	Ожидаем.Что(Результат[1].ИмяПакета).Равно("asserts");
	Ожидаем.Что(Результат[1].ЦелеваяВерсия).Равно("1.5.0");
	Ожидаем.Что(Результат[2].ИмяПакета).Равно("autumn");
	Ожидаем.Что(Результат[2].ЦелеваяВерсия).Равно("4.3.11");
	Ожидаем.Что(Результат[3].ИмяПакета).Равно("delegate");
	Ожидаем.Что(Результат[3].ЦелеваяВерсия).Равно("1.0.0");
	Ожидаем.Что(Результат[4].ИмяПакета).Равно("packageinfo");
	Ожидаем.Что(Результат[4].ЦелеваяВерсия).Равно("0.8");
	Ожидаем.Что(Результат[5].ИмяПакета).Равно("1testrunner");
	Ожидаем.Что(Результат[5].ЦелеваяВерсия).Равно("1.9.2");

КонецПроцедуры

&Тест
Процедура ТестДолжен_ПроверитьНаличиеТолькоУстаревшихПакетов() Экспорт

	// Действие
	Результат = ПолучитьОпределительВерсийЗависимостей()
		.ТолькоУстаревшие()
		.Получить();

	// Проверка
	Ожидаем.Что(Результат).ИмеетДлину(4);
	Ожидаем.Что(Результат[0].ИмяПакета).Равно("annotations");
	Ожидаем.Что(Результат[0].ЦелеваяВерсия).Равно("1.3.1");
	Ожидаем.Что(Результат[1].ИмяПакета).Равно("autumn");
	Ожидаем.Что(Результат[1].ЦелеваяВерсия).Равно("4.3.11");
	Ожидаем.Что(Результат[2].ИмяПакета).Равно("packageinfo");
	Ожидаем.Что(Результат[2].ЦелеваяВерсия).Равно("0.8");
	Ожидаем.Что(Результат[3].ИмяПакета).Равно("1testrunner");
	Ожидаем.Что(Результат[3].ЦелеваяВерсия).Равно("1.9.2");

КонецПроцедуры

&Тест
Процедура ТестДолжен_ПроверитьФильтрПоИмениПакета() Экспорт

	// Подготовка
	ТестовыеСлучаи = Новый Соответствие();
	ТестовыеСлучаи.Вставить("autumn", "autumn");
	ТестовыеСлучаи.Вставить("annotations,autumn, asserts", "annotations,autumn,asserts");
	ТестовыеСлучаи.Вставить("annotations autumn  asserts ", "annotations,autumn,asserts");
	ТестовыеСлучаи.Вставить("autumnnn", "");
	ТестовыеСлучаи.Вставить("autu?n", "autumn");
	ТестовыеСлучаи.Вставить("autu?", "");
	ТестовыеСлучаи.Вставить("a?s", "");
	ТестовыеСлучаи.Вставить("autu*", "autumn");
	ТестовыеСлучаи.Вставить("a*s", "annotations,asserts");
	ТестовыеСлучаи.Вставить("/(ass.*|d)/", "asserts,delegate");
	ТестовыеСлучаи.Вставить("a*s, delegate", "annotations,asserts,delegate");

	Определитель = ПолучитьОпределительВерсийЗависимостей();

	Для Каждого ТестовыйСлучай Из ТестовыеСлучаи Цикл

		Шаблон = ТестовыйСлучай.Ключ;
		ИменаПакетов = СтрРазделить(ТестовыйСлучай.Значение, ",", Ложь);

		// Действие
		Результат = Определитель.ФильтроватьПоИмени(Шаблон).Получить();

		// Проверка
		Ожидаем.Что(Результат, Шаблон).ИмеетДлину(ИменаПакетов.Количество());

		Для Каждого СтрокаРезультата Из Результат Цикл		
			Ожидаем.Что(ИменаПакетов, Шаблон).Содержит(СтрокаРезультата.ИмяПакета);
		КонецЦикла;

	КонецЦикла;

КонецПроцедуры

&Тест
Процедура ТестДолжен_ПроверитьТипВерсии() Экспорт

	// Подготовка
	ТестовыеСлучаи = Новый Соответствие();
	ТестовыеСлучаи.Вставить(
		ТипыВерсий.Последняя, 
		"annotations@1.3.1,asserts@1.5.0,autumn@4.3.11,delegate@1.0.0,packageinfo@0.8,1testrunner@1.9.2"
	);
	ТестовыеСлучаи.Вставить(
		ТипыВерсий.Минорная,
		"annotations@1.3.1,asserts@1.5.0,autumn@3.3.0,delegate@1.0.0,packageinfo@0.8,1testrunner@1.9.2"
	);
	ТестовыеСлучаи.Вставить(
		ТипыВерсий.Патч,
		"annotations@1.3.1,asserts@1.5.0,autumn@3.2.0,delegate@1.0.0,packageinfo@0.7,1testrunner@1.8.0"
	);

	Определитель = ПолучитьОпределительВерсийЗависимостей();

	Для Каждого ТестовыйСлучай Из ТестовыеСлучаи Цикл

		ТипВерсии = ТестовыйСлучай.Ключ;
		ИменаПакетов = СтрРазделить(ТестовыйСлучай.Значение, ",", Ложь);

		// Действие
		Результат = Определитель.УстановитьТипВерсии(ТипВерсии).Получить();

		// Проверка
		Ожидаем.Что(Результат, ТипВерсии).ИмеетДлину(ИменаПакетов.Количество());

		Для Каждого СтрокаРезультата Из Результат Цикл		
			ИмяИВерсия = СтрШаблон("%1@%2", СтрокаРезультата.ИмяПакета, СтрокаРезультата.ЦелеваяВерсия);
			Ожидаем.Что(ИменаПакетов, ТипВерсии).Содержит(ИмяИВерсия);
		КонецЦикла;

	КонецЦикла;

КонецПроцедуры

&Тест
Процедура ТестДолжен_ПроверитьВыбрасываниеИсключенияНеУдалосьПрочитатьЗависимости() Экспорт
		
	// Подготовка
	Определитель = Поделка.НайтиЖелудь("ОпределительВерсийЗависимостей");
	Определитель.ИзФайла("./tests/fixtures/unknown");
	
	// Действие и проверка
	Ожидаем.Что(Определитель)
		.Метод("Получить")
		.ВыбрасываетИсключение("Ошибка чтения файла манифеста './tests/fixtures/unknown'");

КонецПроцедуры

&Тест
Процедура ТестДолжен_ПроверитьВыбрасываниеИсключенияНеУказанМанифест() Экспорт
		
	// Подготовка
	Определитель = Поделка.НайтиЖелудь("ОпределительВерсийЗависимостей");
	
	// Действие и проверка
	Ожидаем.Что(Определитель)
		.Метод("Получить")
		.ВыбрасываетИсключение("Не указан источник зависимостей (файл манифеста или таблица).");

КонецПроцедуры

Функция ПолучитьОпределительВерсийЗависимостей()
	Определитель = Поделка.НайтиЖелудь("ОпределительВерсийЗависимостей");
	Определитель.ИзФайла(ПомощникТестирования.ФайлМанифеста);
	Возврат Определитель;
КонецФункции