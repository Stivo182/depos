// BSLLS:LineLength-off
// BSLLS:MagicNumber-off
// BSLLS:DuplicateStringLiteral-off

#Использовать "helpers"

Перем Поделка; // Поделка

&Инициализация
Процедура Инициализация() Экспорт

	Поделка = Новый Поделка();
	Поделка.ЗапуститьПриложение();

КонецПроцедуры

&После
Процедура ПослеКаждого() Экспорт
	ПомощникТестирования.УдалитьВременныеФайлы();
КонецПроцедуры

&Тест
Процедура ТестДолжен_ОбновитьЗависимостиИзТаблицы() Экспорт

	// Подготовка
	ОбновляторЗависимостей = Поделка.НайтиЖелудь("ОбновляторЗависимостей");
	ФайлМанифеста = ПомощникТестирования.СоздатьВременнуюКопиюФайла(ПомощникТестирования.ФайлМанифеста);

	// Действие
	ОбновляторЗависимостей.Обновить(ФайлМанифеста, ОбновленныеЗависимости());

	// Проверка
	ПомощникТестирования.ОжидаемЧтоФайлыИдентичны(ФайлМанифеста, ПомощникТестирования.ФайлОбновленногоМанифеста);

КонецПроцедуры

&Тест
Процедура ТестДолжен_ПроверитьНеизменностьПриПередачеПустойКоллекцииЗависимостей() Экспорт

	// Подготовка
	ОбновляторЗависимостей = Поделка.НайтиЖелудь("ОбновляторЗависимостей");
	ФайлМанифеста = ПомощникТестирования.СоздатьВременнуюКопиюФайла(ПомощникТестирования.ФайлМанифеста);

	// Действие
	ОбновляторЗависимостей.Обновить(ФайлМанифеста, Новый ЗависимостиПакета());

	// Проверка
	ПомощникТестирования.ОжидаемЧтоФайлыИдентичны(ФайлМанифеста, ПомощникТестирования.ФайлМанифеста);

КонецПроцедуры

&Тест
Процедура Тест_ПроверяетОшибкуПриПередачеОтсутствующейВМанифестеЗависимости() Экспорт

	// Подготовка
	ОбновляторЗависимостей = Поделка.НайтиЖелудь("ОбновляторЗависимостей");
	ФайлМанифеста = ПомощникТестирования.СоздатьВременнуюКопиюФайла(ПомощникТестирования.ФайлМанифеста);
	Зависимости = Новый ЗависимостиПакета().Добавить("unknown", "1.0.0");

	ПараметрыМетода = Новый Массив;
	ПараметрыМетода.Добавить(ФайлМанифеста);
	ПараметрыМетода.Добавить(Зависимости);

	// Действие и проверка
	Ожидаем
		.Что(ОбновляторЗависимостей)
		.Метод("Обновить", ПараметрыМетода)
		.ВыбрасываетИсключение("Не удалось выполнить обновление зависимостей. Проверка зависимостей закончилась неудачей: не найдена зависимость 'unknown' в файле манифеста.");

	// Проверяем содержимое манифеста
	ПомощникТестирования.ОжидаемЧтоФайлыИдентичны(ФайлМанифеста, ПомощникТестирования.ФайлМанифеста);

КонецПроцедуры

&Тест
Процедура ТестДолжен_ПроверитьСохранениеРезервнойКопии() Экспорт

	// Подготовка
	ОбновляторЗависимостей = Поделка.НайтиЖелудь("ОбновляторЗависимостей");
	ФайлМанифеста = ПомощникТестирования.СоздатьВременнуюКопиюФайла(ПомощникТестирования.ФайлМанифеста);
	Зависимости = Новый ЗависимостиПакета().Добавить("autumn", "4.3.11");

	// Действие
	ОбновляторЗависимостей
		.СохранятьКопию()
		.Обновить(ФайлМанифеста, Зависимости);

	// Проверка
	Файл = Новый Файл(ФайлМанифеста);
	Маска = Файл.Имя + "_" + Формат(ТекущаяДата(), "ДФ=yyyyMMdd") + "*";
	НайденныеФайлы = НайтиФайлы(Файл.Путь, Маска);

	// Проверяем наличие резервной копии
	Ожидаем.Что(НайденныеФайлы).Заполнено();

	// Проверяем содержимое резервной копии манифеста
	ПомощникТестирования.ОжидаемЧтоФайлыИдентичны(НайденныеФайлы[0].ПолноеИмя, ПомощникТестирования.ФайлМанифеста);

КонецПроцедуры

#Область СлужебныеПроцедурыИФункции

Функция ОбновленныеЗависимости()

	ИсходныеЗависимости = Новый ЗависимостиПакета().ПрочитатьИзМанифеста(ПомощникТестирования.ФайлМанифеста);

	Зависимости = Поделка.НайтиЖелудь("ОпределительВерсийЗависимостей")
		.ТолькоУстаревшие()
		.НайтиВерсии(ИсходныеЗависимости, ТипыВерсий.Последняя);

	Возврат Зависимости;

КонецФункции

#КонецОбласти