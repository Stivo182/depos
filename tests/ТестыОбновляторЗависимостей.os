// BSLLS:LineLength-off
// BSLLS:MagicNumber-off
// BSLLS:DuplicateStringLiteral-off

#Использовать "helpers"

Перем Поделка; // Поделка

&Инициализация
Процедура Инициализация() Экспорт

	Поделка = Новый Поделка();
	Поделка.ЗапуститьПриложение();

КонецПроцедуры

&После
Процедура ПослеКаждого() Экспорт
	ПомощникТестирования.УдалитьВременныеФайлы();
КонецПроцедуры

&Тест
Процедура ТестДолжен_ОбновитьЗависимостиИзТаблицы() Экспорт

	// Подготовка
	ОбновляторЗависимостей = Поделка.НайтиЖелудь("ОбновляторЗависимостей");

	ФайлМанифеста = ПомощникТестирования.СоздатьВременнуюКопиюФайла(ПомощникТестирования.ФайлМанифеста);

	// Действие
	ОбновляторЗависимостей
		.УстановитьФайлМанифеста(ФайлМанифеста)
		.ДобавитьЗависимости(ОбновленныеЗависимости())
		.Обновить();

	// Проверка
	ПомощникТестирования.ОжидаемЧтоФайлыИдентичны(ФайлМанифеста, ПомощникТестирования.ФайлОбновленногоМанифеста);

КонецПроцедуры

&Тест
Процедура ТестДолжен_ОбновитьЗависимостиПоСтрокам() Экспорт

	// Подготовка
	ОбновляторЗависимостей = Поделка.НайтиЖелудь("ОбновляторЗависимостей");

	ФайлМанифеста = ПомощникТестирования.СоздатьВременнуюКопиюФайла(ПомощникТестирования.ФайлМанифеста);

	// Действие
	ОбновляторЗависимостей
		.УстановитьФайлМанифеста(ФайлМанифеста)
		.ДобавитьЗависимость("annotations", "1.3.1")
		.ДобавитьЗависимость("packageinfo", "0.8")
		.ДобавитьЗависимость("autumn", "4.3.11")
		.ДобавитьЗависимостьДляРазработки("1testrunner", "1.9.2")
		.Обновить();

	// Проверка
	ПомощникТестирования.ОжидаемЧтоФайлыИдентичны(ФайлМанифеста, ПомощникТестирования.ФайлОбновленногоМанифеста);

КонецПроцедуры

&Тест
Процедура ТестДолжен_ПроверитьПовторноеДобавлениеЗависимости() Экспорт

	// Подготовка
	ОбновляторЗависимостей = Поделка.НайтиЖелудь("ОбновляторЗависимостей");

	ФайлМанифеста = ПомощникТестирования.СоздатьВременнуюКопиюФайла(ПомощникТестирования.ФайлМанифеста);

	// Действие
	ОбновляторЗависимостей
		.УстановитьФайлМанифеста(ФайлМанифеста)
		.ДобавитьЗависимости(ОбновленныеЗависимости())
		.ДобавитьЗависимость("delegate", "2.0.0")
		.ДобавитьЗависимость("delegate", "1.0.0")
		.Обновить();

	// Проверка
	ПомощникТестирования.ОжидаемЧтоФайлыИдентичны(ФайлМанифеста, ПомощникТестирования.ФайлОбновленногоМанифеста);

КонецПроцедуры

&Тест
Процедура ТестДолжен_ПроверитьСохранностьПриПередачеПустойВерсии() Экспорт

	// Подготовка
	ОбновляторЗависимостей = Поделка.НайтиЖелудь("ОбновляторЗависимостей");

	ФайлМанифеста = ПомощникТестирования.СоздатьВременнуюКопиюФайла(ПомощникТестирования.ФайлМанифеста);

	// Действие
	ОбновляторЗависимостей
		.УстановитьФайлМанифеста(ФайлМанифеста)
		.ДобавитьЗависимости(ОбновленныеЗависимости())
		.ДобавитьЗависимость("delegate", "")
		.Обновить();

	// Проверка
	ПомощникТестирования.ОжидаемЧтоФайлыИдентичны(ФайлМанифеста, ПомощникТестирования.ФайлОбновленногоМанифеста);

КонецПроцедуры

&Тест
Процедура ТестДолжен_ПроверитьПрерываниеПриОтсутствииПереданныхЗависимостей() Экспорт

	// Подготовка
	ОбновляторЗависимостей = Поделка.НайтиЖелудь("ОбновляторЗависимостей");

	ФайлМанифеста = ПомощникТестирования.СоздатьВременнуюКопиюФайла(ПомощникТестирования.ФайлМанифеста);

	// Действие
	ОбновляторЗависимостей
		.УстановитьФайлМанифеста(ФайлМанифеста)
		.Обновить();

	// Проверка
	ПомощникТестирования.ОжидаемЧтоФайлыИдентичны(ФайлМанифеста, ПомощникТестирования.ФайлМанифеста);

КонецПроцедуры

&Тест
Процедура ТестДолжен_ПроверитьПрерываниеПриОтсутствииЗависимостейВМанифестеИБезПередачиЗависимостей() Экспорт

	// Подготовка
	ОбновляторЗависимостей = Поделка.НайтиЖелудь("ОбновляторЗависимостей");

	ФайлМанифеста = ПомощникТестирования.СоздатьВременнуюКопиюФайла("./tests/fixtures/packagedef-no-deps");

	// Действие
	ОбновляторЗависимостей
		.УстановитьФайлМанифеста(ФайлМанифеста)
		.Обновить();

	// Проверка
	ПомощникТестирования.ОжидаемЧтоФайлыИдентичны(ФайлМанифеста, "./tests/fixtures/packagedef-no-deps");

КонецПроцедуры

&Тест
Процедура ТестДолжен_ПроверитьВыбрасываниеИсключенияПриОтсутствииЗависимостейВМанифестеИПередачеЗависимости() Экспорт

	// Подготовка
	ОбновляторЗависимостей = Поделка.НайтиЖелудь("ОбновляторЗависимостей");

	ФайлМанифеста = ПомощникТестирования.СоздатьВременнуюКопиюФайла("./tests/fixtures/packagedef-no-deps");

	ОбновляторЗависимостей.УстановитьФайлМанифеста(ФайлМанифеста).ДобавитьЗависимость("autumn", "1.0.0");

	// Действие и проверка
	Ожидаем
		.Что(ОбновляторЗависимостей)
		.Метод("Обновить")
		.ВыбрасываетИсключение("Не удалось выполнить обновление зависимостей. Проверка зависимостей закончилась неудачей: не найдена зависимость 'autumn' в файле манифеста.");

	// Проверяем неизменность манифеста
	ПомощникТестирования.ОжидаемЧтоФайлыИдентичны(ФайлМанифеста, "./tests/fixtures/packagedef-no-deps");

КонецПроцедуры

&Тест
Процедура ТестДолжен_ПроверитьВыбрасываниеИсключенияПриПередачеОтсутствующейВМанифестеЗависимости() Экспорт

	// Подготовка
	ОбновляторЗависимостей = Поделка.НайтиЖелудь("ОбновляторЗависимостей");

	ФайлМанифеста = ПомощникТестирования.СоздатьВременнуюКопиюФайла(ПомощникТестирования.ФайлМанифеста);

	ОбновляторЗависимостей.УстановитьФайлМанифеста(ФайлМанифеста).ДобавитьЗависимость("unknown", "1.0.0");

	// Действие и проверка
	Ожидаем
		.Что(ОбновляторЗависимостей)
		.Метод("Обновить")
		.ВыбрасываетИсключение("Не удалось выполнить обновление зависимостей. Проверка зависимостей закончилась неудачей: не найдена зависимость 'unknown' в файле манифеста.");

	// Проверяем содержимое манифеста
	ПомощникТестирования.ОжидаемЧтоФайлыИдентичны(ФайлМанифеста, ПомощникТестирования.ФайлМанифеста);

КонецПроцедуры

&Тест
Процедура ТестДолжен_ПроверитьВыбрасываниеИсключенияКогдаНеУказанМанифест() Экспорт

	// Подготовка
	ОбновляторЗависимостей = Поделка.НайтиЖелудь("ОбновляторЗависимостей");

	// Действие и проверка
	Ожидаем
		.Что(ОбновляторЗависимостей)
		.Метод("Обновить")
		.ВыбрасываетИсключение("Не указан файл манифеста");

КонецПроцедуры

&Тест
Процедура ТестДолжен_ПроверитьВыбрасываниеИсключенияПриПередачеНесуществующегоМанифеста() Экспорт

	// Подготовка
	ОбновляторЗависимостей = Поделка.НайтиЖелудь("ОбновляторЗависимостей");
	ОбновляторЗависимостей.УстановитьФайлМанифеста("./tests/fixtures/unknown");

	// Действие и проверка
	Ожидаем
		.Что(ОбновляторЗависимостей)
		.Метод("Обновить")
		.ВыбрасываетИсключение("Не найден файл манифеста './tests/fixtures/unknown'");

КонецПроцедуры

&Тест
Процедура ТестДолжен_ПроверитьСохранениеРезервнойКопии() Экспорт

	// Подготовка
	ОбновляторЗависимостей = Поделка.НайтиЖелудь("ОбновляторЗависимостей");

	ФайлМанифеста = ПомощникТестирования.СоздатьВременнуюКопиюФайла(ПомощникТестирования.ФайлМанифеста);

	// Действие
	ОбновляторЗависимостей
		.УстановитьФайлМанифеста(ФайлМанифеста)	
		.ДобавитьЗависимость("autumn", "4.3.11")
		.СохранятьКопию()
		.Обновить();

	// Проверка
	Файл = Новый Файл(ФайлМанифеста);
	Маска = Файл.Имя + "_" + Формат(ТекущаяДата(), "ДФ=yyyyMMdd") + "*";
	НайденныеФайлы = НайтиФайлы(Файл.Путь, Маска);

	// Проверяем наличие резервной копии
	Ожидаем.Что(НайденныеФайлы).Заполнено();

	// Проверяем содержимое резервной копии манифеста
	ПомощникТестирования.ОжидаемЧтоФайлыИдентичны(НайденныеФайлы[0].ПолноеИмя, ПомощникТестирования.ФайлМанифеста);

КонецПроцедуры

#Область СлужебныеПроцедурыИФункции

Функция ОбновленныеЗависимости()

	Зависимости = Поделка.НайтиЖелудь("ОпределительВерсийЗависимостей")
		.УстановитьФайлМанифеста(ПомощникТестирования.ФайлМанифеста)
		.ТолькоУстаревшие()
		.УстановитьТипВерсии(ТипыВерсий.Последняя)
		.ПолучитьЗависимости();

	Зависимости.Колонки.Удалить("МинимальнаяВерсия");
	Зависимости.Колонки.ЦелеваяВерсия.Имя = "МинимальнаяВерсия";

	Возврат Зависимости;

КонецФункции

#КонецОбласти