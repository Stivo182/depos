// BSLLS:LineLength-off
// BSLLS:DuplicateStringLiteral-off

#Использовать "helpers"

Перем _Поделка; // Поделка

&Инициализация
Процедура Инициализация() Экспорт

	_Поделка = Новый Поделка();
	_Поделка.ЗапуститьПриложение();

КонецПроцедуры

&Тест
Процедура ТестДолжен_НайтиНеустановленныеПакеты() Экспорт
	
	// Подготовка
	Анализатор = _Поделка.НайтиЖелудь("АнализаторЗависимостей")
		.РабочийКаталог("./tests/fixtures");

	Зависимости = ПолучитьЗависимостиПоУмолчанию();

	// Действие
	Результат = Анализатор.ПроверитьЗависимости(Зависимости);

	// Проверка
	Пакеты = Результат.НеустановленныеПакеты.ВыгрузитьКолонку("ИмяПакета");

	Ожидаем.Что(Пакеты).ИмеетДлину(3);
	Ожидаем.Что(Пакеты).Содержит("asserts");
	Ожидаем.Что(Пакеты).Содержит("packageinfo");
	Ожидаем.Что(Пакеты).Содержит("semver");

КонецПроцедуры

&Тест
Процедура ТестДолжен_НайтиКонфликтыВерсий() Экспорт
	
	// Подготовка
	Анализатор = _Поделка.НайтиЖелудь("АнализаторЗависимостей")
		.РабочийКаталог("./tests/fixtures");

	Зависимости = ПолучитьЗависимостиПоУмолчанию();

	// Действие
	Результат = Анализатор.ПроверитьЗависимости(Зависимости);

	// Проверка
	КонфликтыВерсий = Результат.КонфликтыВерсийУстановленныхПакетов;
	КонфликтыВерсий.Сортировать("ИмяПакета");

	Ожидаем.Что(КонфликтыВерсий).ИмеетДлину(3);

	Ожидаем.Что(КонфликтыВерсий[0].ИмяПакета).Равно("1testrunner");
	Ожидаем.Что(КонфликтыВерсий[0].ДляРазработки).Равно(Истина);
	Ожидаем.Что(КонфликтыВерсий[0].ТребуемаяВерсия).Равно(">=1.8.0");
	Ожидаем.Что(КонфликтыВерсий[0].УстановленнаяВерсия).Равно("1.7.0");

	Ожидаем.Что(КонфликтыВерсий[1].ИмяПакета).Равно("annotations");
	Ожидаем.Что(КонфликтыВерсий[1].ДляРазработки).Равно(Ложь);
	Ожидаем.Что(КонфликтыВерсий[1].ТребуемаяВерсия).Равно(">=1.3.0");
	Ожидаем.Что(КонфликтыВерсий[1].УстановленнаяВерсия).Равно("1.0.0");
	
	Ожидаем.Что(КонфликтыВерсий[2].ИмяПакета).Равно("autumn");
	Ожидаем.Что(КонфликтыВерсий[2].ДляРазработки).Равно(Ложь);
	Ожидаем.Что(КонфликтыВерсий[2].ТребуемаяВерсия).Равно("<=4.4.1");
	Ожидаем.Что(КонфликтыВерсий[2].УстановленнаяВерсия).Равно("5.0.0");

КонецПроцедуры

Функция ПолучитьЗависимостиПоУмолчанию()
	Возврат Новый ЗависимостиПакета().ПрочитатьИзМанифеста("./tests/fixtures/packagedef");
КонецФункции