// BSLLS:LineLength-off
// BSLLS:MagicNumber-off
// BSLLS:DuplicateStringLiteral-off

#Использовать "helpers"
#Использовать packageinfo

&Тест
Процедура ТестДолжен_ПроверитьЧтениеИзФайлаМанифеста() Экспорт
	
	// Подготовка
	Зависимости = Новый ЗависимостиПакета();

	// Действие
	Результат = Зависимости.ПрочитатьИзМанифеста(ПомощникТестирования.ФайлМанифеста).ВТаблицу();

	// Проверка
	Ожидаем.Что(Результат).ИмеетДлину(6);
	Ожидаем.Что(Результат[0].ИмяПакета).Равно("delegate");
	Ожидаем.Что(Результат[0].МинимальнаяВерсия).Равно("1.0.0");
	Ожидаем.Что(Результат[0].МаксимальнаяВерсия).Равно("");
	Ожидаем.Что(Результат[0].ДляРазработки).ЭтоЛожь();
	Ожидаем.Что(Результат[1].ИмяПакета).Равно("annotations");
	Ожидаем.Что(Результат[1].МинимальнаяВерсия).Равно("1.3.0");
	Ожидаем.Что(Результат[1].МаксимальнаяВерсия).Равно("");
	Ожидаем.Что(Результат[1].ДляРазработки).ЭтоЛожь();
	Ожидаем.Что(Результат[2].ИмяПакета).Равно("asserts");
	Ожидаем.Что(Результат[2].МинимальнаяВерсия).Равно("");
	Ожидаем.Что(Результат[2].МаксимальнаяВерсия).Равно("");
	Ожидаем.Что(Результат[2].ДляРазработки).ЭтоЛожь();
	Ожидаем.Что(Результат[3].ИмяПакета).Равно("packageinfo");
	Ожидаем.Что(Результат[3].МинимальнаяВерсия).Равно("0.7");
	Ожидаем.Что(Результат[3].МаксимальнаяВерсия).Равно("");
	Ожидаем.Что(Результат[3].ДляРазработки).ЭтоЛожь();
	Ожидаем.Что(Результат[4].ИмяПакета).Равно("autumn");
	Ожидаем.Что(Результат[4].МинимальнаяВерсия).Равно("3.2.0");
	Ожидаем.Что(Результат[4].МаксимальнаяВерсия).Равно("4.4.1");
	Ожидаем.Что(Результат[4].ДляРазработки).ЭтоЛожь();
	Ожидаем.Что(Результат[5].ИмяПакета).Равно("1testrunner");
	Ожидаем.Что(Результат[5].МинимальнаяВерсия).Равно("1.8.0");
	Ожидаем.Что(Результат[5].МаксимальнаяВерсия).Равно("");
	Ожидаем.Что(Результат[5].ДляРазработки).ЭтоИстина();

КонецПроцедуры

&Тест
Процедура ТестДолжен_ПроверитьЧтениеИзТаблицы() Экспорт
	
	// Подготовка
	Зависимости = Новый ЗависимостиПакета();

	Таблица = Новый ТаблицаЗначений();
	Таблица.Колонки.Добавить("ИмяПакета");
	Таблица.Колонки.Добавить("ДляРазработки");
	Таблица.Колонки.Добавить("МинимальнаяВерсия");
	Таблица.Колонки.Добавить("МаксимальнаяВерсия");

	СтрокаТаблицы = Таблица.Добавить();
	СтрокаТаблицы.ИмяПакета = "delegate";
	СтрокаТаблицы.МинимальнаяВерсия = "1.0.0";
	СтрокаТаблицы.МаксимальнаяВерсия = "";
	СтрокаТаблицы.ДляРазработки = Ложь;

	СтрокаТаблицы = Таблица.Добавить();
	СтрокаТаблицы.ИмяПакета = "1testrunner";
	СтрокаТаблицы.МинимальнаяВерсия = "1.8.0";
	СтрокаТаблицы.МаксимальнаяВерсия = "";
	СтрокаТаблицы.ДляРазработки = Истина;

	// Действие
	Результат = Зависимости.ПрочитатьИзТаблицы(Таблица).ВТаблицу();

	// Проверка
	Ожидаем.Что(Результат).ИмеетДлину(2);
	Ожидаем.Что(Результат[0].ИмяПакета).Равно("delegate");
	Ожидаем.Что(Результат[0].МинимальнаяВерсия).Равно("1.0.0");
	Ожидаем.Что(Результат[0].МаксимальнаяВерсия).Равно("");
	Ожидаем.Что(Результат[0].ДляРазработки).ЭтоЛожь();
	Ожидаем.Что(Результат[1].ИмяПакета).Равно("1testrunner");
	Ожидаем.Что(Результат[1].МинимальнаяВерсия).Равно("1.8.0");
	Ожидаем.Что(Результат[1].МаксимальнаяВерсия).Равно("");
	Ожидаем.Что(Результат[1].ДляРазработки).ЭтоИстина();

КонецПроцедуры

&Тест
Процедура ТестДолжен_ПроверитьДобавлениеЗависимостейПострочно() Экспорт
	
	// Подготовка
	Зависимости = Новый ЗависимостиПакета();

	// Действие
	Результат = Зависимости
		.Добавить("delegate", "1.0.0")
		.Добавить("1testrunner", "1.8.0", "1.9.0", Истина)
		.ДобавитьДляРазработки("asserts", "1.5.0")
		.Добавить("autumn")
		.ВТаблицу();

	// Проверка
	Ожидаем.Что(Результат).ИмеетДлину(4);
	Ожидаем.Что(Результат[0].ИмяПакета).Равно("delegate");
	Ожидаем.Что(Результат[0].МинимальнаяВерсия).Равно("1.0.0");
	Ожидаем.Что(Результат[0].МаксимальнаяВерсия).Равно("");
	Ожидаем.Что(Результат[0].ДляРазработки).ЭтоЛожь();
	Ожидаем.Что(Результат[1].ИмяПакета).Равно("1testrunner");
	Ожидаем.Что(Результат[1].МинимальнаяВерсия).Равно("1.8.0");
	Ожидаем.Что(Результат[1].МаксимальнаяВерсия).Равно("1.9.0");
	Ожидаем.Что(Результат[1].ДляРазработки).ЭтоИстина();
	Ожидаем.Что(Результат[2].ИмяПакета).Равно("asserts");
	Ожидаем.Что(Результат[2].МинимальнаяВерсия).Равно("1.5.0");
	Ожидаем.Что(Результат[2].МаксимальнаяВерсия).Равно("");
	Ожидаем.Что(Результат[2].ДляРазработки).ЭтоИстина();
	Ожидаем.Что(Результат[3].ИмяПакета).Равно("autumn");
	Ожидаем.Что(Результат[3].МинимальнаяВерсия).Равно("");
	Ожидаем.Что(Результат[3].МаксимальнаяВерсия).Равно("");
	Ожидаем.Что(Результат[3].ДляРазработки).ЭтоЛожь();

КонецПроцедуры

&Тест
Процедура ТестДолжен_ПроверитьФильтрПоИмениПакета() Экспорт

	// Подготовка
	ТестовыеСлучаи = Новый Соответствие();
	ТестовыеСлучаи.Вставить("autumn", "autumn");
	ТестовыеСлучаи.Вставить("annotations,autumn, asserts", "annotations,autumn,asserts");
	ТестовыеСлучаи.Вставить("annotations autumn  asserts ", "annotations,autumn,asserts");
	ТестовыеСлучаи.Вставить("autumnnn", "");
	ТестовыеСлучаи.Вставить("autu?n", "autumn");
	ТестовыеСлучаи.Вставить("autu?", "");
	ТестовыеСлучаи.Вставить("a?s", "");
	ТестовыеСлучаи.Вставить("autu*", "autumn");
	ТестовыеСлучаи.Вставить("a*s", "annotations,asserts");
	ТестовыеСлучаи.Вставить("/(ass.*|d)/", "asserts,delegate");
	ТестовыеСлучаи.Вставить("a*s, delegate", "annotations,asserts,delegate");

	ТаблицаЗависимостей = Новый ИнформацияОПакете(ПомощникТестирования.ФайлМанифеста).Зависимости();
	
	Для Каждого ТестовыйСлучай Из ТестовыеСлучаи Цикл

		Шаблон = ТестовыйСлучай.Ключ;
		ИменаПакетов = СтрРазделить(ТестовыйСлучай.Значение, ",", Ложь);

		Зависимости = Новый ЗависимостиПакета().ПрочитатьИзТаблицы(ТаблицаЗависимостей);

		// Действие
		Результат = Зависимости.ФильтроватьПоИмени(Шаблон).ВТаблицу();

		// Проверка
		Ожидаем.Что(Результат, Шаблон).ИмеетДлину(ИменаПакетов.Количество());

		Для Каждого СтрокаРезультата Из Результат Цикл		
			Ожидаем.Что(ИменаПакетов, Шаблон).Содержит(СтрокаРезультата.ИмяПакета);
		КонецЦикла;

	КонецЦикла;

КонецПроцедуры

&Тест
Процедура Тест_ПроверяетОшибкуКогдаВозниклаОшибкаПриПолученииЗависимостей() Экспорт
		
	// Подготовка
	Зависимости = Новый ЗависимостиПакета();
	
	ПараметрыМетода = Новый Массив();
	ПараметрыМетода.Добавить("./tests/fixtures/packagedef-syntax-error");
	
	// Действие и проверка
	Ожидаем.Что(Зависимости)
		.Метод("ПрочитатьИзМанифеста", ПараметрыМетода)
		.ВыбрасываетИсключение("Возникла ошибка при получении зависимостей из файла манифеста './tests/fixtures/packagedef-syntax-error'");

КонецПроцедуры