#Использовать autumn
#Использовать asserts
#Использовать fs
#Использовать "../src/core"
#Использовать "../src/internal"

Перем _ТекущийКаталог; // Строка
Перем _ПеременныеСредыКонфигурация; // Строка

&Инициализация
Процедура Инициализация() Экспорт
	_ТекущийКаталог = ТекущийКаталог();
	_ПеременныеСредыКонфигурация = ПолучитьПеременнуюСреды("OSCRIPT_CONFIG");
КонецПроцедуры

&После
Процедура ПослеКаждого() Экспорт
	УстановитьТекущийКаталог(_ТекущийКаталог);
	УстановитьПеременнуюСреды("OSCRIPT_CONFIG", _ПеременныеСредыКонфигурация);
КонецПроцедуры

&Тест
Процедура ТестДолжен_НайтиПакеты() Экспорт

	// Подготовка
	УстановитьТекущийКаталог("./tests");

	КаталогБиблиотек1 = "../tests/fixtures/lib_sets/lib1";
	КаталогБиблиотек2 = "../tests/fixtures/lib_sets/lib2";

	ТестовыеСлучаи = Новый ТаблицаЗначений();
	ТестовыеСлучаи.Колонки.Добавить("ПеременнаяСреды_ОсновноеКаталог");
	ТестовыеСлучаи.Колонки.Добавить("ПеременнаяСреды_ДополнительныйКаталог");
	ТестовыеСлучаи.Колонки.Добавить("КаталогБиблиотек");
	ТестовыеСлучаи.Колонки.Добавить("Пакеты");

	ДобавитьТестовыйСлучай(ТестовыеСлучаи, 
		КаталогБиблиотек1, 
		"", 
		"",
		"lib1/fs;lib3/package3");

	ДобавитьТестовыйСлучай(ТестовыеСлучаи, 
		"", 
		КаталогБиблиотек2, 
		"",
		"oscript_modules/fs;lib2/package2");

	ДобавитьТестовыйСлучай(ТестовыеСлучаи, 
		КаталогБиблиотек1, 
		КаталогБиблиотек2, 
		"",
		"lib1/fs;lib1/package1;lib2/package2");

	ДобавитьТестовыйСлучай(ТестовыеСлучаи, 
		"", 
		"", 
		"",
		"oscript_modules/fs;lib3/package3");

	ДобавитьТестовыйСлучай(ТестовыеСлучаи, 
		"", 
		"", 
		КаталогБиблиотек2,
		"lib2/fs");

	ДобавитьТестовыйСлучай(ТестовыеСлучаи, 
		КаталогБиблиотек1, 
		"", 
		КаталогБиблиотек2,
		"lib2/fs");

	ДобавитьТестовыйСлучай(ТестовыеСлучаи, 
		"", 
		КаталогБиблиотек1, 
		КаталогБиблиотек2,
		"lib2/fs;lib1/package1");

	Для Каждого ТестовыйСлучай Из ТестовыеСлучаи Цикл
		
		УстановитьКаталогиБиблиотекВПеременнуюСреды(
			ТестовыйСлучай.ПеременнаяСреды_ОсновноеКаталог, 
			ТестовыйСлучай.ПеременнаяСреды_ДополнительныйКаталог);

		Сканер = Новый СканерУстановленныхПакетов();

		// Действие
		Результат = Сканер.НайтиВсеПакеты(ТестовыйСлучай.КаталогБиблиотек);

		// Утверждение
		Для Каждого Пакет Из ТестовыйСлучай.Пакеты Цикл
			КаталогПакетаСодержит(Результат, Пакет.ИмяКаталога, Пакет.ИмяПакета);
		КонецЦикла;

	КонецЦикла;

КонецПроцедуры

Процедура ДобавитьТестовыйСлучай(ТестовыеСлучаи, 
	ПеременнаяСреды_ОсновноеКаталог, ПеременнаяСреды_ДополнительныйКаталог, КаталогБиблиотек, Пакеты)

	ТестовыйСлучай = ТестовыеСлучаи.Добавить();
	ТестовыйСлучай.ПеременнаяСреды_ОсновноеКаталог = ПеременнаяСреды_ОсновноеКаталог;
	ТестовыйСлучай.ПеременнаяСреды_ДополнительныйКаталог = ПеременнаяСреды_ДополнительныйКаталог;
	ТестовыйСлучай.КаталогБиблиотек = КаталогБиблиотек;
	ТестовыйСлучай.Пакеты = Новый Массив();

	Для Каждого Строка Из СтрРазделить(Пакеты, ";") Цикл
		Части = СтрРазделить(Строка, "/");
		ТестовыйСлучай.Пакеты.Добавить(Новый Структура("ИмяКаталога, ИмяПакета", Части[0], Части[1]));
	КонецЦикла;

КонецПроцедуры

Процедура КаталогПакетаСодержит(Результат, ИмяКаталога, ИмяПакета)
	Ожидаем.Что(Строка(Результат[ИмяПакета]), СтрШаблон("%1/%2", ИмяКаталога, ИмяПакета)).Содержит(ИмяКаталога);
КонецПроцедуры

Процедура УстановитьКаталогиБиблиотекВПеременнуюСреды(ОсновнойКаталог = "", ДополнительныйКаталог = "")
	
	Параметры = Новый Массив();

	Если ЗначениеЗаполнено(ОсновнойКаталог) Тогда
		Параметры.Добавить("lib.system=" + ОсновнойКаталог);
	КонецЕсли;

	Если ЗначениеЗаполнено(ДополнительныйКаталог) Тогда
		Параметры.Добавить("lib.additional=" + ДополнительныйКаталог);
	КонецЕсли;

	УстановитьПеременнуюСреды("OSCRIPT_CONFIG", СтрСоединить(Параметры, ";"));

КонецПроцедуры