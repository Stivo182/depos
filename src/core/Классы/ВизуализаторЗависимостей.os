#Использовать coloratos
#Использовать semver

#Область Конструктор

&Желудь
Процедура ПриСозданииОбъекта()
КонецПроцедуры

#КонецОбласти

#Область ПрограммныйИнтерфейс

// Выводит таблицу зависимостей в консоль с цветовым форматированием.
// Версии отображаются с цветовым выделением: красный для основной версии,
// зеленый для минорной, бирюзовый для патч-версии.
//
// Параметры:
//   Зависимости - ТаблицаЗначений - Таблица с данными о зависимостях:
//     * ИмяПакета - Строка - Имя пакета
//     * ДляРазработки - Булево - Признак зависимости для разработки
//     * МинимальнаяВерсия - Строка - Текущая минимальная версия
//     * ЦелеваяВерсия - Строка - Рекомендуемая целевая версия
Процедура Вывести(Зависимости) Экспорт

	ТекстДляРазработкиПоУмолчанию = "dev";

	ДлиныКолонок = МаксимальнаяДлинаКолонок("ИмяПакета, МинимальнаяВерсия", Зависимости);

	Для Каждого СтрокаЗависимости Из Зависимости Цикл

		МинВерсия = ?(ЗначениеЗаполнено(СтрокаЗависимости.МинимальнаяВерсия), СтрокаЗависимости.МинимальнаяВерсия, "*");
		ТекстДляРазработки = ?(СтрокаЗависимости.ДляРазработки, ТекстДляРазработкиПоУмолчанию, "");

		// Имя пакета
		Консоль.Вывести(ФиксированнаяСтрока(СтрокаЗависимости.ИмяПакета, ДлиныКолонок.ИмяПакета, , , Истина));

		// Признак для разработки
		Консоль.Вывести(ФиксированнаяСтрока(ТекстДляРазработки, СтрДлина(ТекстДляРазработкиПоУмолчанию), , , Истина));

		Консоль.Вывести(" ");

		// Текущая версия
		Консоль.Вывести(ФиксированнаяСтрока(МинВерсия, ДлиныКолонок.МинимальнаяВерсия));

		Консоль.Вывести(" → ");

		// Целевая версия
		ВывестиЦелевуюВерсию(СтрокаЗависимости.ЦелеваяВерсия, СтрокаЗависимости.МинимальнаяВерсия);

	КонецЦикла;

КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Процедура ВывестиЦелевуюВерсию(ЦелеваяВерсия, МинимальнаяВерсия)
	
	ЦветМажорнойЧасти = "ТемноКрасный";
	ЦветМинорнойЧасти = "Зеленый";
	ЦветПатчЧасти = "ТемноБирюзовый";
	ПустаяВерсия = "-.-.-";

	МинимальнаяВерсияОбъект = Новый Версия(МинимальнаяВерсия);
	ЦелеваяВерсияОбъект = Новый Версия(ЦелеваяВерсия);
	ЕстьПатчВерсия = СтрЧислоВхождений(ЦелеваяВерсия, ".") > 1;

	ОбычнаяЧасть = "";
	ЦветнаяЧасть = "";
	Цвет = "Серый";
	
	Если Не ЗначениеЗаполнено(ЦелеваяВерсия) Тогда
		ЦветнаяЧасть = ПустаяВерсия;
	ИначеЕсли Не ЗначениеЗаполнено(МинимальнаяВерсия) Тогда
		ЦветнаяЧасть = ЦелеваяВерсия;
	ИначеЕсли МинимальнаяВерсияОбъект.Основная <> ЦелеваяВерсияОбъект.Основная Тогда
		ЦветнаяЧасть = ЦелеваяВерсия;
		Цвет = ЦветМажорнойЧасти;
	ИначеЕсли МинимальнаяВерсияОбъект.Второстепенная <> ЦелеваяВерсияОбъект.Второстепенная Тогда
		ОбычнаяЧасть = СтрШаблон("%1.", ЦелеваяВерсияОбъект.Основная);

		Если ЕстьПатчВерсия Тогда
			ЦветнаяЧасть = СтрШаблон("%1.%2", ЦелеваяВерсияОбъект.Второстепенная, ЦелеваяВерсияОбъект.Патч);
		Иначе
			ЦветнаяЧасть = ЦелеваяВерсияОбъект.Второстепенная;
		КонецЕсли;

		Цвет = ЦветМинорнойЧасти;
	ИначеЕсли ЕстьПатчВерсия И МинимальнаяВерсияОбъект.Патч <> ЦелеваяВерсияОбъект.Патч Тогда
		ОбычнаяЧасть = СтрШаблон("%1.%2.", ЦелеваяВерсияОбъект.Основная, ЦелеваяВерсияОбъект.Второстепенная);
		ЦветнаяЧасть = ЦелеваяВерсияОбъект.Патч;
		Цвет = ЦветПатчЧасти;
	Иначе
		ОбычнаяЧасть = ЦелеваяВерсия;
	КонецЕсли;
	
	Если ЗначениеЗаполнено(ЦветнаяЧасть) Тогда
		Текст = СтрШаблон("%1(%2|#color=%3)", ОбычнаяЧасть, ЦветнаяЧасть, Цвет);
	Иначе
		Текст = ОбычнаяЧасть;
	КонецЕсли;

	ЦветнойВывод.ВывестиСтроку(Текст);

КонецПроцедуры

Функция МаксимальнаяДлинаКолонок(Колонки, Таблица)
	
	Результат = Новый Структура(Колонки);

	Для Каждого Строка Из Результат Цикл
		Результат[Строка.Ключ] = МаксимальнаяДлинаВКолонке(Строка.Ключ, Таблица);
	КонецЦикла;

	Возврат Результат;

КонецФункции

Функция МаксимальнаяДлинаВКолонке(ИмяКолонки, Таблица)
	
	МаксДлина = 0;

	Для Каждого Строка Из Таблица Цикл
		МаксДлина = Макс(МаксДлина, СтрДлина(Строка[ИмяКолонки]));
	КонецЦикла;

	Возврат МаксДлина;
	
КонецФункции

Функция ФиксированнаяСтрока(
	Знач Строка, 
	Знач ТребуемаяДлина,
	ПоложениеПраво = Ложь,
	ЗаполняемыйСимвол = " ",
	ОборачиватьЗаполняемымСимволом = Ложь)
	
	КоличествоСимволов = ТребуемаяДлина - СтрДлина(Строка);
	Если КоличествоСимволов < 0 Тогда
		Возврат Строка;
	КонецЕсли;

	ДобавочныеСимволы = Новый Массив();
	Для Инд = 1 По КоличествоСимволов Цикл
		ДобавочныеСимволы.Добавить(ЗаполняемыйСимвол);
	КонецЦикла;
	
	ДобавочныеСимволы = СтрСоединить(ДобавочныеСимволы);

	Если ПоложениеПраво Тогда
		Строка = "" + ДобавочныеСимволы + Строка;
	Иначе
		Строка = "" + Строка + ДобавочныеСимволы;
	КонецЕсли;
	
	Если ОборачиватьЗаполняемымСимволом Тогда
		Строка = ЗаполняемыйСимвол + Строка + ЗаполняемыйСимвол;
	КонецЕсли;

	Возврат Строка;

КонецФункции

#КонецОбласти