#Использовать coloratos
#Использовать semver

&Пластилин("НастройкиПриложения")
Перем _НастройкиПриложения; // НастройкиПриложения

#Область Конструктор

&Желудь
Процедура ПриСозданииОбъекта()
КонецПроцедуры

#КонецОбласти

#Область ПрограммныйИнтерфейс

// Выводит таблицу зависимостей в консоль в режиме проверки доступных обновлений.
//
// Параметры:
//   Зависимости - ТаблицаЗначений - Таблица с данными о зависимостях:
//     * ИмяПакета - Строка - Имя пакета
//     * ДляРазработки - Булево - Признак зависимости для разработки
//     * МинимальнаяВерсия - Строка - Текущая минимальная версия
//     * ЦелеваяВерсия - Строка - Целевая целевая версия
Процедура ВывестиКакПроверенные(Зависимости) Экспорт
	Вывести(Зависимости, Ложь);
КонецПроцедуры

// Выводит таблицу зависимостей в консоль в режиме подтверждения выполненных обновлений.
//
// Параметры:
//   Зависимости - ТаблицаЗначений - Таблица с данными о зависимостях:
//     * ИмяПакета - Строка - Имя пакета
//     * ДляРазработки - Булево - Признак зависимости для разработки
//     * МинимальнаяВерсия - Строка - Текущая минимальная версия
//     * ЦелеваяВерсия - Строка - Целевая целевая версия
Процедура ВывестиКакОбновленные(Зависимости) Экспорт
	Вывести(Зависимости, Истина);
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Процедура Вывести(Зависимости, Обновлено) Экспорт

	ДлиныКолонок = МаксимальнаяДлинаКолонок("ИмяПакета, МинимальнаяВерсия", Зависимости);

	Для Каждого СтрокаЗависимости Из Зависимости Цикл
		ВывестиСтрокуПакета(СтрокаЗависимости, ДлиныКолонок, Обновлено);
	КонецЦикла;

КонецПроцедуры

Процедура ВывестиСтрокуПакета(СтрокаЗависимости, ДлиныКолонок, Обновлено)
	
	МинВерсия = ?(ЗначениеЗаполнено(СтрокаЗависимости.МинимальнаяВерсия), СтрокаЗависимости.МинимальнаяВерсия, "*");
	ТекстДляРазработки = ?(СтрокаЗависимости.ДляРазработки, _НастройкиПриложения.ФорматированиеТекстДляРазработки, "");
	ТребуетсяОбновление = ТребуетсяОбновлениеДляВерсии(
		СтрокаЗависимости.МинимальнаяВерсия, 
		СтрокаЗависимости.ЦелеваяВерсия
	);
	СимволОбновления = ?(Обновлено, 
		_НастройкиПриложения.ФорматированиеСимволВыполненногоОбновления,
		_НастройкиПриложения.ФорматированиеСимволНаличияОбновления
	);
	ЦветСимволаОбновления = ?(Обновлено, 
		_НастройкиПриложения.ФорматированиеЦветСимволаВыполненногоОбновления,
		_НастройкиПриложения.ФорматированиеЦветСимволаНаличияОбновления
	);
	
	Консоль.Вывести(" ");

	// Символ обновления
	ЦветнойВывод.Вывести(?(ТребуетсяОбновление, СимволОбновления, " "), ЦветСимволаОбновления);
	
	// Имя пакета
	Консоль.Вывести(ФиксированнаяСтрока(СтрокаЗависимости.ИмяПакета, ДлиныКолонок.ИмяПакета, , , Истина));

	// Признак для разработки
	ТекстДляРазработкиФорматированный = ФиксированнаяСтрока(
		ТекстДляРазработки, 
		СтрДлина(_НастройкиПриложения.ФорматированиеТекстДляРазработки), 
		, 
		, 
		Истина
	);
	Консоль.Вывести(ТекстДляРазработкиФорматированный);

	Консоль.Вывести(" ");

	// Текущая версия
	Консоль.Вывести(ФиксированнаяСтрока(МинВерсия, ДлиныКолонок.МинимальнаяВерсия));

	Консоль.Вывести(" → ");

	// Целевая версия
	ВывестиЦелевуюВерсию(СтрокаЗависимости.ЦелеваяВерсия, СтрокаЗависимости.МинимальнаяВерсия);

КонецПроцедуры

Процедура ВывестиЦелевуюВерсию(ЦелеваяВерсия, МинимальнаяВерсия)

	МинимальнаяВерсияОбъект = Новый Версия(МинимальнаяВерсия);
	ЦелеваяВерсияОбъект = Новый Версия(ЦелеваяВерсия);
	ЕстьПатчВерсия = СтрЧислоВхождений(ЦелеваяВерсия, ".") > 1;

	ОбычнаяЧасть = "";
	ЦветнаяЧасть = "";
	Цвет = "Серый";
	
	Если Не ЗначениеЗаполнено(ЦелеваяВерсия) Тогда
		ЦветнаяЧасть = _НастройкиПриложения.ФорматированиеПустаяВерсия;
	ИначеЕсли Не ЗначениеЗаполнено(МинимальнаяВерсия) Тогда
		ЦветнаяЧасть = ЦелеваяВерсия;
	ИначеЕсли МинимальнаяВерсияОбъект.Основная <> ЦелеваяВерсияОбъект.Основная Тогда
		Цвет = _НастройкиПриложения.ФорматированиеЦветМажорнойЧасти;
		ЦветнаяЧасть = ЦелеваяВерсия;
	ИначеЕсли МинимальнаяВерсияОбъект.Второстепенная <> ЦелеваяВерсияОбъект.Второстепенная Тогда
		Цвет = _НастройкиПриложения.ФорматированиеЦветМинорнойЧасти;
		ОбычнаяЧасть = СтрШаблон("%1.", ЦелеваяВерсияОбъект.Основная);

		Если ЕстьПатчВерсия Тогда
			ЦветнаяЧасть = СтрШаблон("%1.%2", ЦелеваяВерсияОбъект.Второстепенная, ЦелеваяВерсияОбъект.Патч);
		Иначе
			ЦветнаяЧасть = ЦелеваяВерсияОбъект.Второстепенная;
		КонецЕсли;
	ИначеЕсли ЕстьПатчВерсия И МинимальнаяВерсияОбъект.Патч <> ЦелеваяВерсияОбъект.Патч Тогда
		Цвет = _НастройкиПриложения.ФорматированиеЦветПатчЧасти;
		ОбычнаяЧасть = СтрШаблон("%1.%2.", ЦелеваяВерсияОбъект.Основная, ЦелеваяВерсияОбъект.Второстепенная);
		ЦветнаяЧасть = ЦелеваяВерсияОбъект.Патч;
	Иначе
		ОбычнаяЧасть = ЦелеваяВерсия;
	КонецЕсли;
	
	Если ЗначениеЗаполнено(ЦветнаяЧасть) Тогда
		Текст = СтрШаблон("%1(%2|#color=%3)", ОбычнаяЧасть, ЦветнаяЧасть, Цвет);
	Иначе
		Текст = ОбычнаяЧасть;
	КонецЕсли;

	ЦветнойВывод.ВывестиСтроку(Текст);

КонецПроцедуры

Функция МаксимальнаяДлинаКолонок(Колонки, Таблица)
	
	Результат = Новый Структура(Колонки);

	Для Каждого Строка Из Результат Цикл
		Результат[Строка.Ключ] = МаксимальнаяДлинаВКолонке(Строка.Ключ, Таблица);
	КонецЦикла;

	Возврат Результат;

КонецФункции

Функция МаксимальнаяДлинаВКолонке(ИмяКолонки, Таблица)
	
	МаксДлина = 0;

	Для Каждого Строка Из Таблица Цикл
		МаксДлина = Макс(МаксДлина, СтрДлина(Строка[ИмяКолонки]));
	КонецЦикла;

	Возврат МаксДлина;
	
КонецФункции

Функция ФиксированнаяСтрока(
	Знач Строка, 
	Знач ТребуемаяДлина,
	ПоложениеПраво = Ложь,
	ЗаполняемыйСимвол = " ",
	ОборачиватьЗаполняемымСимволом = Ложь)
	
	КоличествоСимволов = ТребуемаяДлина - СтрДлина(Строка);
	Если КоличествоСимволов < 0 Тогда
		Возврат Строка;
	КонецЕсли;

	ДобавочныеСимволы = Новый Массив();
	Для Инд = 1 По КоличествоСимволов Цикл
		ДобавочныеСимволы.Добавить(ЗаполняемыйСимвол);
	КонецЦикла;
	
	ДобавочныеСимволы = СтрСоединить(ДобавочныеСимволы);

	Если ПоложениеПраво Тогда
		Строка = "" + ДобавочныеСимволы + Строка;
	Иначе
		Строка = "" + Строка + ДобавочныеСимволы;
	КонецЕсли;
	
	Если ОборачиватьЗаполняемымСимволом Тогда
		Строка = ЗаполняемыйСимвол + Строка + ЗаполняемыйСимвол;
	КонецЕсли;

	Возврат Строка;

КонецФункции

Функция ТребуетсяОбновлениеДляВерсии(МинимальнаяВерсия, ЦелеваяВерсия)

	Возврат ЗначениеЗаполнено(ЦелеваяВерсия) 
		И ЗначениеЗаполнено(МинимальнаяВерсия)
		И Версии.ВерсияБольше(ЦелеваяВерсия, Новый Версия(МинимальнаяВерсия));

КонецФункции

#КонецОбласти