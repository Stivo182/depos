#Использовать semver
#Использовать "../../internal"

&Пластилин("НастройкиФорматирования")
Перем _НастройкиФорматирования; // НастройкиФорматирования

#Область Конструктор

&Желудь
Процедура ПриСозданииОбъекта()
КонецПроцедуры

#КонецОбласти

#Область ПрограммныйИнтерфейс

// Выводит таблицу изменений зависимостей в консоль в режиме проверки доступных обновлений.
//
// Параметры:
//   ИзмененияЗависимостей - ТаблицаЗначений - см. ЗависимостиПакета.ВычислитьИзменения
Процедура ВывестиКакПроверенные(ИзмененияЗависимостей) Экспорт
	Вывести(ИзмененияЗависимостей, Ложь);
КонецПроцедуры

// Выводит таблицу изменений зависимостей в консоль в режиме подтверждения выполненных обновлений.
//
// Параметры:
//   ИзмененияЗависимостей - ТаблицаЗначений - см. ЗависимостиПакета.ВычислитьИзменения
Процедура ВывестиКакОбновленные(ИзмененияЗависимостей) Экспорт
	Вывести(ИзмененияЗависимостей, Истина);
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Процедура Вывести(ИзмененияЗависимостей, Обновлено)

	ИзмененияЗависимостей.Сортировать("ДляРазработки, ИмяПакета");

	ТаблицаВывода = Новый ТаблицаЗначений();
	ТаблицаВывода.Колонки.Добавить("Префикс", , "");
	ТаблицаВывода.Колонки.Добавить("ИмяПакета", , "Пакет");
	ТаблицаВывода.Колонки.Добавить("ДляРазработки", , "");
	ТаблицаВывода.Колонки.Добавить("ВерсияДо", , "До");
	ТаблицаВывода.Колонки.Добавить("Стрелка", , "");
	ТаблицаВывода.Колонки.Добавить("ВерсияПосле", , "После");

	КонсольныйВыводТаблицы = Новый КонсольныйВыводТаблицы();
	КонсольныйВыводТаблицы.УстановитьТаблицу(ТаблицаВывода);

	НастройктВывода = КонсольныйВыводТаблицы.ПолучитьНастройки();
	НастройктВывода.ОтступСлева = 1;
	НастройктВывода.ВыводитьЗаголовки = Ложь;
	НастройктВывода.Колонки.Префикс.Отступ = 0;
	НастройктВывода.Колонки.Стрелка.Отступ = 0;

	Для Каждого СтрокаИзменения Из ИзмененияЗависимостей Цикл
		ДобавитьСтрокуВывода(ТаблицаВывода, СтрокаИзменения, Обновлено);
	КонецЦикла;

	КонсольныйВыводТаблицы.УстановитьНастройки(НастройктВывода);
	КонсольныйВыводТаблицы.Вывести();

КонецПроцедуры

Процедура ДобавитьСтрокуВывода(ТаблицаВывода, СтрокаИзменения, Обновлено)

	СтрокаВывода = ТаблицаВывода.Добавить();

	ВерсияДо = СтрокаИзменения.МинимальнаяВерсияДо;
	ВерсияПосле = СтрокаИзменения.МинимальнаяВерсияПосле;
	
	ТекущаяВерсия = ?(ЗначениеЗаполнено(ВерсияДо), ВерсияДо, "*");
	ТекстДляРазработки = ?(СтрокаИзменения.ДляРазработки, _НастройкиФорматирования.ТекстДляРазработки, "");
	ТребуетсяОбновление = ТребуетсяОбновлениеДляВерсии(
		ВерсияДо, 
		ВерсияПосле
	);
	СимволОбновления = ?(Обновлено, НаборСимволов.ОК, НаборСимволов.СтрелкаВверх);
	ЦветСимволаОбновления = ?(Обновлено, 
		_НастройкиФорматирования.ЦветСимволаВыполненногоОбновления,
		_НастройкиФорматирования.ЦветСимволаНаличияОбновления
	);

	СтрокаВывода.Префикс = ПомощникКонсольногоВывода.ПокраситьТекст(
		?(ТребуетсяОбновление, СимволОбновления, " "),
		ЦветСимволаОбновления
	);
	СтрокаВывода.ИмяПакета = СтрокаИзменения.ИмяПакета;
	СтрокаВывода.ДляРазработки = ТекстДляРазработки;
	СтрокаВывода.ВерсияДо = ТекущаяВерсия;
	СтрокаВывода.Стрелка = НаборСимволов.СтрелкаВправо;
	СтрокаВывода.ВерсияПосле = ЦелеваяВерсияДляВывода(ВерсияПосле, ВерсияДо);

КонецПроцедуры

Функция ЦелеваяВерсияДляВывода(ЦелеваяВерсия, ИсходнаяВерсия)

	ИсходнаяВерсияОбъект = Новый Версия(ИсходнаяВерсия);
	ЦелеваяВерсияОбъект = Новый Версия(ЦелеваяВерсия);
	ЕстьПатчВерсия = СтрЧислоВхождений(ЦелеваяВерсия, ".") > 1;

	ОбычнаяЧасть = "";
	ЦветнаяЧасть = "";
	Цвет = "Серый";
	
	Если Не ЗначениеЗаполнено(ЦелеваяВерсия) Тогда
		ЦветнаяЧасть = _НастройкиФорматирования.ПустаяВерсия;
	ИначеЕсли Не ЗначениеЗаполнено(ИсходнаяВерсия) Тогда
		ЦветнаяЧасть = ЦелеваяВерсия;
	ИначеЕсли ИсходнаяВерсияОбъект.Основная <> ЦелеваяВерсияОбъект.Основная Тогда
		Цвет = _НастройкиФорматирования.ЦветМажорнойЧасти;
		ЦветнаяЧасть = ЦелеваяВерсия;
	ИначеЕсли ИсходнаяВерсияОбъект.Второстепенная <> ЦелеваяВерсияОбъект.Второстепенная Тогда
		Цвет = _НастройкиФорматирования.ЦветМинорнойЧасти;
		ОбычнаяЧасть = СтрШаблон("%1.", ЦелеваяВерсияОбъект.Основная);

		Если ЕстьПатчВерсия Тогда
			ЦветнаяЧасть = СтрШаблон("%1.%2", ЦелеваяВерсияОбъект.Второстепенная, ЦелеваяВерсияОбъект.Патч);
		Иначе
			ЦветнаяЧасть = ЦелеваяВерсияОбъект.Второстепенная;
		КонецЕсли;
	ИначеЕсли ЕстьПатчВерсия И ИсходнаяВерсияОбъект.Патч <> ЦелеваяВерсияОбъект.Патч Тогда
		Цвет = _НастройкиФорматирования.ЦветПатчЧасти;
		ОбычнаяЧасть = СтрШаблон("%1.%2.", ЦелеваяВерсияОбъект.Основная, ЦелеваяВерсияОбъект.Второстепенная);
		ЦветнаяЧасть = ЦелеваяВерсияОбъект.Патч;
	Иначе
		ОбычнаяЧасть = ЦелеваяВерсия;
	КонецЕсли;
	
	Если ЗначениеЗаполнено(ЦветнаяЧасть) Тогда
		Текст = СтрШаблон("%1%2",
			ОбычнаяЧасть, 
			ПомощникКонсольногоВывода.ПокраситьТекст(ЦветнаяЧасть, Цвет)
		);
	Иначе
		Текст = ОбычнаяЧасть;
	КонецЕсли;

	Возврат Текст;

КонецФункции

Функция ТребуетсяОбновлениеДляВерсии(ИсходнаяВерсия, ЦелеваяВерсия)

	Возврат ЗначениеЗаполнено(ЦелеваяВерсия) 
		И ЗначениеЗаполнено(ИсходнаяВерсия)
		И Версии.ВерсияБольше(ЦелеваяВерсия, Новый Версия(ИсходнаяВерсия));

КонецФункции

#КонецОбласти