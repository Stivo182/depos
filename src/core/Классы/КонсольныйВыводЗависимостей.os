#Использовать coloratos
#Использовать semver
#Использовать "../../internal"

&Пластилин("НастройкиПриложения")
Перем _НастройкиПриложения; // НастройкиПриложения

#Область Конструктор

&Желудь
Процедура ПриСозданииОбъекта()
КонецПроцедуры

#КонецОбласти

#Область ПрограммныйИнтерфейс

// Выводит таблицу зависимостей в консоль в режиме проверки доступных обновлений.
//
// Параметры:
//   ЦелевыеЗависимости - ЗависимостиПакета - Целевые зависимости пакета
//   ИсходныеЗависимости - ЗависимостиПакета - Исходные зависимости пакета
Процедура ВывестиКакПроверенные(ЦелевыеЗависимости, ИсходныеЗависимости) Экспорт
	Вывести(ЦелевыеЗависимости, ИсходныеЗависимости, Ложь);
КонецПроцедуры

// Выводит таблицу зависимостей в консоль в режиме подтверждения выполненных обновлений.
//
// Параметры:
//   ЦелевыеЗависимости - ЗависимостиПакета - Целевые зависимости пакета
//   ИсходныеЗависимости - ЗависимостиПакета - Исходные зависимости пакета
Процедура ВывестиКакОбновленные(ЦелевыеЗависимости, ИсходныеЗависимости) Экспорт
	Вывести(ЦелевыеЗависимости, ИсходныеЗависимости, Истина);
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Процедура Вывести(ЦелевыеЗависимости, ИсходныеЗависимости, Обновлено)

	ТаблицаЦелевые = ЦелевыеЗависимости.ВТаблицу();
	ТаблицаЦелевые.Сортировать("ДляРазработки, ИмяПакета");

	ТаблицаИсходные = ИсходныеЗависимости.ВТаблицу();

	ДлиныКолонок = Новый Структура(
		"ИмяПакета, ИсходнаяВерсия", 
		МаксимальнаяДлинаВКолонке("ИмяПакета", ТаблицаЦелевые), 
		МаксимальнаяДлинаВКолонке("МинимальнаяВерсия", ТаблицаИсходные)
	);

	Для Каждого СтрокаЦелевая Из ТаблицаЦелевые Цикл

		СтрокаИсходная = ТаблицаИсходные.Найти(СтрокаЦелевая.ИмяПакета, "ИмяПакета");
		ИсходнаяВерсия = ?(СтрокаИсходная = Неопределено, "", СтрокаИсходная.МинимальнаяВерсия);

		ВывестиСтрокуПакета(СтрокаЦелевая, ИсходнаяВерсия, ДлиныКолонок, Обновлено);

	КонецЦикла;

КонецПроцедуры

Процедура ВывестиСтрокуПакета(СтрокаЗависимости, ИсходнаяВерсия, ДлиныКолонок, Обновлено)
	
	МинВерсия = ?(ЗначениеЗаполнено(ИсходнаяВерсия), ИсходнаяВерсия, "*");
	ТекстДляРазработки = ?(СтрокаЗависимости.ДляРазработки, _НастройкиПриложения.ФорматированиеТекстДляРазработки, "");
	ТребуетсяОбновление = ТребуетсяОбновлениеДляВерсии(
		ИсходнаяВерсия, 
		СтрокаЗависимости.МинимальнаяВерсия
	);
	СимволОбновления = ?(Обновлено, НаборСимволов.ОК, НаборСимволов.СтрелкаВверх);
	ЦветСимволаОбновления = ?(Обновлено, 
		_НастройкиПриложения.ФорматированиеЦветСимволаВыполненногоОбновления,
		_НастройкиПриложения.ФорматированиеЦветСимволаНаличияОбновления
	);
	
	Консоль.Вывести(" ");

	// Символ обновления
	ЦветнойВывод.Вывести(?(ТребуетсяОбновление, СимволОбновления, " "), ЦветСимволаОбновления);
	
	// Имя пакета
	Консоль.Вывести(ФиксированнаяСтрока(СтрокаЗависимости.ИмяПакета, ДлиныКолонок.ИмяПакета, , , Истина));

	// Признак для разработки
	ТекстДляРазработкиФорматированный = ФиксированнаяСтрока(
		ТекстДляРазработки, 
		СтрДлина(_НастройкиПриложения.ФорматированиеТекстДляРазработки), 
		, 
		, 
		Истина
	);
	Консоль.Вывести(ТекстДляРазработкиФорматированный);

	Консоль.Вывести(" ");

	// Текущая версия
	Консоль.Вывести(ФиксированнаяСтрока(МинВерсия, ДлиныКолонок.ИсходнаяВерсия));

	Консоль.Вывести(СтрШаблон(" %1 ", НаборСимволов.СтрелкаВправо));

	// Целевая версия
	ВывестиЦелевуюВерсию(СтрокаЗависимости.МинимальнаяВерсия, ИсходнаяВерсия);

КонецПроцедуры

Процедура ВывестиЦелевуюВерсию(ЦелеваяВерсия, ИсходнаяВерсия)

	ИсходнаяВерсияОбъект = Новый Версия(ИсходнаяВерсия);
	ЦелеваяВерсияОбъект = Новый Версия(ЦелеваяВерсия);
	ЕстьПатчВерсия = СтрЧислоВхождений(ЦелеваяВерсия, ".") > 1;

	ОбычнаяЧасть = "";
	ЦветнаяЧасть = "";
	Цвет = "Серый";
	
	Если Не ЗначениеЗаполнено(ЦелеваяВерсия) Тогда
		ЦветнаяЧасть = _НастройкиПриложения.ФорматированиеПустаяВерсия;
	ИначеЕсли Не ЗначениеЗаполнено(ИсходнаяВерсия) Тогда
		ЦветнаяЧасть = ЦелеваяВерсия;
	ИначеЕсли ИсходнаяВерсияОбъект.Основная <> ЦелеваяВерсияОбъект.Основная Тогда
		Цвет = _НастройкиПриложения.ФорматированиеЦветМажорнойЧасти;
		ЦветнаяЧасть = ЦелеваяВерсия;
	ИначеЕсли ИсходнаяВерсияОбъект.Второстепенная <> ЦелеваяВерсияОбъект.Второстепенная Тогда
		Цвет = _НастройкиПриложения.ФорматированиеЦветМинорнойЧасти;
		ОбычнаяЧасть = СтрШаблон("%1.", ЦелеваяВерсияОбъект.Основная);

		Если ЕстьПатчВерсия Тогда
			ЦветнаяЧасть = СтрШаблон("%1.%2", ЦелеваяВерсияОбъект.Второстепенная, ЦелеваяВерсияОбъект.Патч);
		Иначе
			ЦветнаяЧасть = ЦелеваяВерсияОбъект.Второстепенная;
		КонецЕсли;
	ИначеЕсли ЕстьПатчВерсия И ИсходнаяВерсияОбъект.Патч <> ЦелеваяВерсияОбъект.Патч Тогда
		Цвет = _НастройкиПриложения.ФорматированиеЦветПатчЧасти;
		ОбычнаяЧасть = СтрШаблон("%1.%2.", ЦелеваяВерсияОбъект.Основная, ЦелеваяВерсияОбъект.Второстепенная);
		ЦветнаяЧасть = ЦелеваяВерсияОбъект.Патч;
	Иначе
		ОбычнаяЧасть = ЦелеваяВерсия;
	КонецЕсли;
	
	Если ЗначениеЗаполнено(ЦветнаяЧасть) Тогда
		Текст = СтрШаблон("%1(%2|#color=%3)", ОбычнаяЧасть, ЦветнаяЧасть, Цвет);
	Иначе
		Текст = ОбычнаяЧасть;
	КонецЕсли;

	ЦветнойВывод.ВывестиСтроку(Текст);

КонецПроцедуры

Функция МаксимальнаяДлинаВКолонке(ИмяКолонки, Таблица)
	
	МаксДлина = 0;

	Для Каждого Строка Из Таблица Цикл
		МаксДлина = Макс(МаксДлина, СтрДлина(Строка[ИмяКолонки]));
	КонецЦикла;

	Возврат МаксДлина;
	
КонецФункции

Функция ФиксированнаяСтрока(
	Знач Строка, 
	Знач ТребуемаяДлина,
	ПоложениеПраво = Ложь,
	ЗаполняемыйСимвол = " ",
	ОборачиватьЗаполняемымСимволом = Ложь)
	
	КоличествоСимволов = ТребуемаяДлина - СтрДлина(Строка);
	Если КоличествоСимволов < 0 Тогда
		Возврат Строка;
	КонецЕсли;

	ДобавочныеСимволы = Новый Массив();
	Для Инд = 1 По КоличествоСимволов Цикл
		ДобавочныеСимволы.Добавить(ЗаполняемыйСимвол);
	КонецЦикла;
	
	ДобавочныеСимволы = СтрСоединить(ДобавочныеСимволы);

	Если ПоложениеПраво Тогда
		Строка = "" + ДобавочныеСимволы + Строка;
	Иначе
		Строка = "" + Строка + ДобавочныеСимволы;
	КонецЕсли;
	
	Если ОборачиватьЗаполняемымСимволом Тогда
		Строка = ЗаполняемыйСимвол + Строка + ЗаполняемыйСимвол;
	КонецЕсли;

	Возврат Строка;

КонецФункции

Функция ТребуетсяОбновлениеДляВерсии(ИсходнаяВерсия, ЦелеваяВерсия)

	Возврат ЗначениеЗаполнено(ЦелеваяВерсия) 
		И ЗначениеЗаполнено(ИсходнаяВерсия)
		И Версии.ВерсияБольше(ЦелеваяВерсия, Новый Версия(ИсходнаяВерсия));

КонецФункции

#КонецОбласти