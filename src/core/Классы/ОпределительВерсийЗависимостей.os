#Использовать semver
#Использовать packageinfo

#Область ОписаниеПеременных

Перем _Зависимости; // ТаблицаЗначений, Неопределено
Перем _ТолькоУстаревшие; // Булево
Перем _ТипВерсии; // Строка, Неопределено
Перем _ИмяПакета; // Строка, Неопределено
Перем _РегулярноеВыражениеСовпаденияИмени; // РегулярноеВыражение, Неопределено
Перем _ИмяФайлаМанифеста; // Строка, Неопределено

&Пластилин("ОпределительВерсийПакетов")
Перем _ОпределительВерсийПакетов; // ОпределительВерсийПакетов

#КонецОбласти

#Область Конструктор

&Желудь
&Характер("Компанейский")
Процедура ПриСозданииОбъекта()

	ТолькоУстаревшие(Ложь);
	УстановитьТипВерсии(ТипыВерсий.Последняя);

КонецПроцедуры

#КонецОбласти

#Область ПрограммныйИнтерфейс

// Устанавливает зависимости из переданной таблицы.
//
// Параметры:
//   Зависимости - ТаблицаЗначений - Таблица с данными о зависимостях пакетов:
//     * ИмяПакета - Строка
//     * ДляРазработки - Булево
//     * МинимальнаяВерсия - Строка
//     * МаксимальнаяВерсия - Строка
//
// Возвращаемое значение:
//   ЭтотОбъект - Для поддержки цепочки вызовов
Функция ИзТаблицы(Зависимости) Экспорт
	_Зависимости = Зависимости.Скопировать(, "ИмяПакета, ДляРазработки, МинимальнаяВерсия, МаксимальнаяВерсия");
	_ИмяФайлаМанифеста = Неопределено;
	Возврат ЭтотОбъект;
КонецФункции

// Устанавливает путь к файлу манифеста для чтения зависимостей.
//
// Параметры:
//   Файл - Строка - Путь к файлу манифеста
//
// Возвращаемое значение:
//   ЭтотОбъект - Для поддержки цепочки вызовов
Функция ИзФайла(Файл) Экспорт
	_ИмяФайлаМанифеста = Файл;
	_Зависимости = Неопределено;
	Возврат ЭтотОбъект;
КонецФункции

// Устанавливает фильтр по устаревшим пакетам.
//
// Параметры:
//   Включить - Булево - Если Истина, возвращаются только пакеты с устаревшими версиями
//
// Возвращаемое значение:
//   ЭтотОбъект - Для поддержки цепочки вызовов
Функция ТолькоУстаревшие(Включить = Истина) Экспорт
	_ТолькоУстаревшие = Включить;
	Возврат ЭтотОбъект;
КонецФункции

// Устанавливает фильтр по имени пакета.
//
// Параметры:
//   ИмяПакета - Строка - Имена пакетов через запятую, маска (поддерживает * и ?)
//                        или регулярное выражение (например, /^autumn-.*$/).
//
// Возвращаемое значение:
//   ЭтотОбъект - Для поддержки цепочки вызовов
Функция ФильтроватьПоИмени(Знач ИмяПакета) Экспорт
	_ИмяПакета = ИмяПакета;
	_РегулярноеВыражениеСовпаденияИмени = Неопределено;
	Возврат ЭтотОбъект;
КонецФункции

// Устанавливает тип целевой версии для определения версий пакетов.
//
// Параметры:
//   ТипВерсии - Строка - Тип версии (см. ТипыВерсий)
//
// Возвращаемое значение:
//   ЭтотОбъект - Для поддержки цепочки вызовов
Функция УстановитьТипВерсии(ТипВерсии) Экспорт
	_ТипВерсии = ТипВерсии;
	Возврат ЭтотОбъект;
КонецФункции

// Возвращает отфильтрованные зависимости с целевыми версиями.
//
// Возвращаемое значение:
//   ТаблицаЗначений - Таблица с колонками:
//     * ИмяПакета - Строка
//     * ДляРазработки - Булево
//     * МинимальнаяВерсия - Строка
//     * МаксимальнаяВерсия - Строка
//     * ЦелеваяВерсия - Строка
Функция Получить() Экспорт

	Если ЗначениеЗаполнено(_ИмяФайлаМанифеста) Тогда
		ПрочитатьЗависимостиИзМанифеста();
	КонецЕсли;

	Если _Зависимости = Неопределено Тогда
		ВызватьИсключение "Не указан источник зависимостей (файл манифеста или таблица).";
	КонецЕсли;

	Если _РегулярноеВыражениеСовпаденияИмени = Неопределено Тогда
		ПодготовитьРегулярноеВыражениеНаСовпадениеИмениПакета();
	КонецЕсли;

	ОтобранныеЗависимости = ПолучитьОтобранныеЗависимости();
	ЦелевыеВерсииПакетов = _ОпределительВерсийПакетов.НайтиВерсии(ОтобранныеЗависимости, _ТипВерсии);

	Результат = ПустаяТаблицаРезультата();

	Для Каждого СтрокаЗависимости Из ОтобранныеЗависимости Цикл

		Версия = ЦелевыеВерсииПакетов[СтрокаЗависимости.ИмяПакета];

		ВерсияБольшеМинимальной = ЗначениеЗаполнено(Версия) 
			И ЗначениеЗаполнено(СтрокаЗависимости.МинимальнаяВерсия)
			И Версии.ВерсияБольше(Версия, Новый Версия(СтрокаЗависимости.МинимальнаяВерсия));
		
		Если Не _ТолькоУстаревшие Или ВерсияБольшеМинимальной Тогда
			СтрокаРезультат = Результат.Добавить();
			ЗаполнитьЗначенияСвойств(СтрокаРезультат, СтрокаЗависимости);
			СтрокаРезультат.ЦелеваяВерсия = Версия;
		КонецЕсли;

	КонецЦикла;

	Результат.Сортировать("ДляРазработки, ИмяПакета");

	Возврат Результат;

КонецФункции

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Процедура ПрочитатьЗависимостиИзМанифеста()
			
	Попытка		
		ИзТаблицы(Новый ИнформацияОПакете(_ИмяФайлаМанифеста).Зависимости());
	Исключение
		ВызватьИсключение СтрШаблон(
			"Ошибка чтения файла манифеста '%1': %2",
			_ИмяФайлаМанифеста,
			ПодробноеПредставлениеОшибки(ИнформацияОбОшибке())
		);
	КонецПопытки;

КонецПроцедуры

Функция ПолучитьОтобранныеЗависимости() Экспорт

	ОтобранныеЗависимости = _Зависимости.Скопировать(Новый Массив());

	Для Каждого СтрокаТаблицы Из _Зависимости Цикл
		Если ИмяПакетаСовпадает(СтрокаТаблицы.ИмяПакета) Тогда
			ЗаполнитьЗначенияСвойств(ОтобранныеЗависимости.Добавить(), СтрокаТаблицы);
		КонецЕсли;
	КонецЦикла;

	Возврат ОтобранныеЗависимости;

КонецФункции

Функция ИмяПакетаСовпадает(ИмяПакета)
	
	Если _РегулярноеВыражениеСовпаденияИмени = Неопределено Тогда
		Возврат Истина;
	КонецЕсли;

	Возврат _РегулярноеВыражениеСовпаденияИмени.Совпадает(ИмяПакета);

КонецФункции

Функция ПустаяТаблицаРезультата()
	
	ТипСтрока = Новый ОписаниеТипов("Строка");

	Результат = Новый ТаблицаЗначений();
	Результат.Колонки.Добавить("ИмяПакета", ТипСтрока);
	Результат.Колонки.Добавить("ДляРазработки", Новый ОписаниеТипов("Булево"));
	Результат.Колонки.Добавить("МинимальнаяВерсия", ТипСтрока);
	Результат.Колонки.Добавить("МаксимальнаяВерсия", ТипСтрока);
	Результат.Колонки.Добавить("ЦелеваяВерсия", ТипСтрока);

	Возврат Результат;

КонецФункции

Процедура ПодготовитьРегулярноеВыражениеНаСовпадениеИмениПакета()

	Если Не ЗначениеЗаполнено(_ИмяПакета) Тогда
		Возврат;
	КонецЕсли;

	ИмяПакета = СокрЛП(_ИмяПакета);
	ЭтоРегулярноеВыражение = Лев(ИмяПакета, 1) = "/" И Прав(ИмяПакета, 1) = "/";

	Паттерн = "";
	Если ЭтоРегулярноеВыражение Тогда
		Паттерн = Сред(ИмяПакета, 2, СтрДлина(ИмяПакета) - 2);
	Иначе
		Паттерн = ЭкранироватьСлужебныеСимволыRegEx(ИмяПакета);
		Паттерн = СтрЗаменить(Паттерн, " ", "");
		Паттерн = СтрЗаменить(Паттерн, "\*", ".*");
		Паттерн = СтрЗаменить(Паттерн, "\?", ".?");
		Паттерн = СтрЗаменить(Паттерн, ",", "|");
		Паттерн = СтрШаблон("^(%1)$", Паттерн);
	КонецЕсли;
	
	_РегулярноеВыражениеСовпаденияИмени = Новый РегулярноеВыражение(Паттерн);

КонецПроцедуры

Функция ЭкранироватьСлужебныеСимволыRegEx(Строка)
	РегулярноеВыражение = Новый РегулярноеВыражение("[\\^$.*+?()[\]{}|]");
	Возврат РегулярноеВыражение.Заменить(Строка, "\$0");
КонецФункции

#КонецОбласти