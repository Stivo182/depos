#Использовать semver
#Использовать "../../internal"

#Область ОписаниеПеременных

Перем _ТолькоУстаревшие; // Булево

&Пластилин("ОпределительВерсийПакетов")
Перем _ОпределительВерсийПакетов; // ОпределительВерсийПакетов

#КонецОбласти

#Область Конструктор

&Желудь
&Характер("Компанейский")
Процедура ПриСозданииОбъекта()
	ТолькоУстаревшие(Ложь);
КонецПроцедуры

#КонецОбласти

#Область ПрограммныйИнтерфейс

// Получает зависимости с целевыми версиями.
//
// Параметры:
//   Зависимости - ЗависимостиПакета
//   ТипВерсии - Строка - Тип целевой версии (см. ТипыВерсий).
//                        Если не указан или пустой, используется значение "Последняя".
//
// Возвращаемое значение:
//   ЗависимостиПакета
Функция НайтиВерсии(Зависимости, ТипВерсии = Неопределено) Экспорт

	ТаблицаЗависимостей = Зависимости.ВТаблицу();
	ЦелевыеВерсииПакетов = _ОпределительВерсийПакетов.НайтиВерсии(ТаблицаЗависимостей, ТипВерсии);

	Результат = Новый ЗависимостиПакета();

	Для Каждого СтрокаЗависимости Из ТаблицаЗависимостей Цикл

		Версия = ЦелевыеВерсииПакетов[СтрокаЗависимости.ИмяПакета];

		ВерсияБольшеИсходной = ЗначениеЗаполнено(Версия) 
			И ЗначениеЗаполнено(СтрокаЗависимости.МинимальнаяВерсия)
			И Версии.ВерсияБольше(Версия, Новый Версия(СтрокаЗависимости.МинимальнаяВерсия));
		
		Если Не _ТолькоУстаревшие Или ВерсияБольшеИсходной Тогда
			Результат.Добавить(
				СтрокаЗависимости.ИмяПакета,
				Версия,
				СтрокаЗависимости.МаксимальнаяВерсия,
				СтрокаЗависимости.ДляРазработки
			);
		КонецЕсли;

	КонецЦикла;

	Возврат Результат;

КонецФункции

// Устанавливает фильтр по устаревшим пакетам.
//
// Параметры:
//   Включить - Булево - Если Истина, возвращаются только пакеты с устаревшими версиями
//
// Возвращаемое значение:
//   ЭтотОбъект - Для поддержки цепочки вызовов
Функция ТолькоУстаревшие(Включить = Истина) Экспорт
	_ТолькоУстаревшие = Включить;
	Возврат ЭтотОбъект;
КонецФункции

#КонецОбласти