#Использовать coloratos
#Использовать "../../internal"

&Пластилин("НастройкиФорматирования")
Перем _НастройкиФорматирования; // НастройкиФорматирования

#Область Конструктор

&Желудь
Процедура ПриСозданииОбъекта()
КонецПроцедуры

#КонецОбласти

#Область ПрограммныйИнтерфейс

// Выводит результаты анализа зависимостей
//
// Параметры:
//   РезультатыАнализа - РезультатыАнализаЗависимостей
Процедура Вывести(РезультатыАнализа) Экспорт
	
	ВывестиОтсутствующиеВМанифестеПакеты(РезультатыАнализа);
	ВывестиНеиспользуемыеПакеты(РезультатыАнализа);
	ВывестиНеустановленныеПакеты(РезультатыАнализа);
	ВывестиКонфликтыВерсийУстановленныхПакетов(РезультатыАнализа);
	ВывестиВердикт(РезультатыАнализа);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Процедура ВывестиОтсутствующиеВМанифестеПакеты(РезультатыАнализа)
	
	Если РезультатыАнализа.ОтсутствующиеВМанифестеПакеты.Количество() = 0 Тогда
		Возврат;	
	КонецЕсли;

	ЦветнойВывод.ВывестиСтроку("ОТСУТСТВУЮЩИЕ В МАНИФЕСТЕ:" + Символы.ПС, "Красный");

	КонсольныйВыводСписков = Новый КонсольныйВыводСписков();

	НастройкиВывода = КонсольныйВыводСписков.ПолучитьНастройки();
	НастройкиВывода.ОтступСлева = 6;

	КонсольныйВыводСписков.УстановитьНастройки(НастройкиВывода);
	
	Для Каждого Пакет Из РезультатыАнализа.ОтсутствующиеВМанифестеПакеты Цикл

		ВывестиПакет(Пакет);
		
		КонсольныйВыводСписков.УстановитьСписок(Пакет.Файлы);
		КонсольныйВыводСписков.Вывести();

	КонецЦикла;

	Консоль.ВывестиСтроку(" ");
	
КонецПроцедуры

Процедура ВывестиНеиспользуемыеПакеты(РезультатыАнализа)
		
	Если РезультатыАнализа.НеиспользуемыеПакеты.Количество() = 0 Тогда
		Возврат;	
	КонецЕсли;

	ЦветнойВывод.ВывестиСтроку("НЕИСПОЛЬЗУЕМЫЕ В КОДЕ" + Символы.ПС, "ТемноЖелтый");
	
	Для Каждого Пакет Из РезультатыАнализа.НеиспользуемыеПакеты Цикл
		ВывестиПакет(Пакет);
	КонецЦикла;

	Консоль.ВывестиСтроку(" ");
	
КонецПроцедуры

Процедура ВывестиНеустановленныеПакеты(РезультатыАнализа)
		
	Если РезультатыАнализа.НеустановленныеПакеты.Количество() = 0 Тогда
		Возврат;	
	КонецЕсли;

	ЦветнойВывод.ВывестиСтроку("НЕ УСТАНОВЛЕНЫ" + Символы.ПС, "ТемноЖелтый");
	
	Для Каждого Пакет Из РезультатыАнализа.НеустановленныеПакеты Цикл
		ВывестиПакет(Пакет);
	КонецЦикла;

	Консоль.ВывестиСтроку(" ");
	
КонецПроцедуры

Процедура ВывестиКонфликтыВерсийУстановленныхПакетов(РезультатыАнализа)
		
	Конфликты = РезультатыАнализа.КонфликтыВерсийУстановленныхПакетов;

	Если Конфликты.Количество() = 0 Тогда
		Возврат;	
	КонецЕсли;

	ЦветнойВывод.ВывестиСтроку("НЕСООТВЕТСТВИЕ ВЕРСИЙ" + Символы.ПС, "ТемноЖелтый");

	ТаблицаВывода = Новый ТаблицаЗначений();
	ТаблицаВывода.Колонки.Добавить("ИмяПакета", , "Пакет");
	ТаблицаВывода.Колонки.Добавить("ДляРазработки", , "");
	ТаблицаВывода.Колонки.Добавить("УстановленнаяВерсия", , "Установленная");
	ТаблицаВывода.Колонки.Добавить("ТребуемаяВерсия", , "Требуемая");
	ТаблицаВывода.Колонки.Добавить("Каталог", , "Источник");

	КонсольныйВыводТаблицы = Новый КонсольныйВыводТаблицы();
	КонсольныйВыводТаблицы.УстановитьТаблицу(ТаблицаВывода);

	Для Каждого СтрокаТаблицы Из Конфликты Цикл
		
		СтрокаВывода = ТаблицаВывода.Добавить();
		ЗаполнитьЗначенияСвойств(СтрокаВывода, СтрокаТаблицы);

		СтрокаВывода.ДляРазработки = ?(СтрокаТаблицы.ДляРазработки, 
			ПомощникКонсольногоВывода.ПокраситьТекст(_НастройкиФорматирования.ТекстДляРазработки, "Желтый")
			, ""
		);

	КонецЦикла;
	
	КонсольныйВыводТаблицы.Вывести();

	Консоль.ВывестиСтроку(" ");
	
КонецПроцедуры

Процедура ВывестиПакет(Пакет)

	Если Пакет.ДляРазработки Тогда
		ТекстДляРазработки = _НастройкиФорматирования.ТекстДляРазработки;
		ТекстВывода = СтрШаблон("   %1 ((%2)|#color=Желтый)", Пакет.ИмяПакета, ТекстДляРазработки);
		ЦветнойВывод.ВывестиСтроку(ТекстВывода);
	Иначе
		Консоль.ВывестиСтроку(СтрШаблон("   %1", Пакет.ИмяПакета));
	КонецЕсли;
	
КонецПроцедуры

Процедура ВывестиВердикт(РезультатыАнализа)
	
	Если Не РезультатыАнализа.ЕстьПроблемы() Тогда
		ЦветнойВывод.ВывестиСтроку(СтрШаблон("%1 Все зависимости корректны!", НаборСимволов.ОК), "Зеленый");
	Иначе
		Консоль.ВывестиСтроку(СтрШаблон("Найдено проблем: %1", РезультатыАнализа.КоличествоПроблем()));
	КонецЕсли;

КонецПроцедуры

#КонецОбласти
