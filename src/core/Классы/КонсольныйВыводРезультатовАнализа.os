#Использовать coloratos
#Использовать "../../internal"

&Пластилин("НастройкиФорматирования")
Перем _НастройкиФорматирования; // НастройкиФорматирования

#Область Конструктор

&Желудь
Процедура ПриСозданииОбъекта()
КонецПроцедуры

#КонецОбласти

#Область ПрограммныйИнтерфейс

// Выводит результаты анализа зависимостей
//
// Параметры:
//   РезультатАнализа - РезультатАнализа
Процедура Вывести(РезультатАнализа) Экспорт
	
	ВывестиОтсутствующиеПакеты(РезультатАнализа);
	ВывестиНеиспользуемыеПакеты(РезультатАнализа);
	ВывестиВердикт(РезультатАнализа);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Процедура ВывестиОтсутствующиеПакеты(РезультатАнализа)
	
	Если РезультатАнализа.ОтсутствующиеПакеты.Количество() = 0 Тогда
		Возврат;	
	КонецЕсли;

	ЦветнойВывод.ВывестиСтроку("Отсутствующие пакеты в манифесте:" + Символы.ПС, "Красный");
	
	Для Каждого Пакет Из РезультатАнализа.ОтсутствующиеПакеты Цикл

		ВывестиПакет(Пакет);
		
		Для Каждого Файл Из Пакет.Файлы Цикл
			Консоль.ВывестиСтроку(СтрШаблон("     - %1", Файл));
		КонецЦикла;

	КонецЦикла;

	Консоль.ВывестиСтроку(" ");
	
КонецПроцедуры

Процедура ВывестиНеиспользуемыеПакеты(РезультатАнализа)
		
	Если РезультатАнализа.НеиспользуемыеПакеты.Количество() = 0 Тогда
		Возврат;	
	КонецЕсли;

	ЦветнойВывод.ВывестиСтроку("Неиспользуемые пакеты в коде:" + Символы.ПС, "ТемноЖелтый");
	
	Для Каждого Пакет Из РезультатАнализа.НеиспользуемыеПакеты Цикл
		ВывестиПакет(Пакет);
	КонецЦикла;

	Консоль.ВывестиСтроку(" ");
	
КонецПроцедуры

Процедура ВывестиПакет(Пакет)

	Если Пакет.ДляРазработки Тогда
		ТекстДляРазработки = _НастройкиФорматирования.ТекстДляРазработки;
		ТекстВывода = СтрШаблон("   %1 ((%2)|#color=Желтый)", Пакет.ИмяПакета, ТекстДляРазработки);
		ЦветнойВывод.ВывестиСтроку(ТекстВывода);
	Иначе
		Консоль.ВывестиСтроку(СтрШаблон("   %1", Пакет.ИмяПакета));
	КонецЕсли;
	
КонецПроцедуры

Процедура ВывестиВердикт(РезультатАнализа)
	
	Если Не РезультатАнализа.ЕстьПроблемы() Тогда
		ЦветнойВывод.ВывестиСтроку(СтрШаблон("%1 Все зависимости корректны!", НаборСимволов.ОК), "Зеленый");
	Иначе
		Консоль.ВывестиСтроку(СтрШаблон("Найдено проблем: %1", РезультатАнализа.КоличествоПроблем()));
	КонецЕсли;

КонецПроцедуры

#КонецОбласти
