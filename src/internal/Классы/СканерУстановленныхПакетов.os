#Использовать collectionos

Перем _ИмяПараметраКаталогаБиблиотек; // Строка
Перем _ИмяПараметраДополнительныхКаталоговБиблиотек; // Строка
Перем _ИмяПеременнойСредыКонфигурации; // Строка

#Область Конструктор

&Желудь
Процедура ПриСозданииОбъекта()

	_ИмяПараметраКаталогаБиблиотек = "lib.system";
	_ИмяПараметраДополнительныхКаталоговБиблиотек = "lib.additional";
	_ИмяПеременнойСредыКонфигурации = "OSCRIPT_CONFIG";

КонецПроцедуры

#КонецОбласти

#Область ПрограммныйИнтерфейс

Функция НайтиВсеПакеты(КаталогБиблиотек = "") Экспорт

	НайденныеПакеты = Новый Соответствие();

	КаталогиБиблиотек = ПолучитьКаталогиБиблиотек(КаталогБиблиотек);

	Для Каждого Каталог Из КаталогиБиблиотек Цикл
		НайтиПакетыВКаталоге(Каталог, НайденныеПакеты);
	КонецЦикла;

	Возврат НайденныеПакеты;

КонецФункции

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Функция ПолучитьКаталогиБиблиотек(КаталогБиблиотек)
	
	Результат = Новый Массив();

	ПараметрыКонфигурации = ПрочитатьПараметрыКонфигурации(КаталогБиблиотек);

	ЗначенияПараметров = Новый Массив();

	Если ЗначениеЗаполнено(КаталогБиблиотек) Тогда
		ЗначенияПараметров.Добавить(КаталогБиблиотек);
	Иначе
		ЗначенияПараметров.Добавить(ПараметрыКонфигурации[_ИмяПараметраКаталогаБиблиотек]);
	КонецЕсли;

	ЗначенияПараметров.Добавить(ПараметрыКонфигурации[_ИмяПараметраДополнительныхКаталоговБиблиотек]);

	Для Каждого Значение Из ЗначенияПараметров Цикл

		Если Не ЗначениеЗаполнено(Значение) Тогда
			Продолжить;
		КонецЕсли;

		Каталоги = СтрРазделить(Значение, ";", Ложь);
		Для Каждого Каталог Из Каталоги Цикл
			Результат.Добавить(Каталог);
		КонецЦикла;

	КонецЦикла;

	Возврат Результат;

КонецФункции

Процедура НайтиПакетыВКаталоге(Каталог, НайденныеПакеты)

	Если Не ФС.КаталогСуществует(Каталог) Тогда
		Возврат;
	КонецЕсли;

	Файлы = НайтиФайлы(Каталог, "*");

	Для Каждого Файл Из Файлы Цикл

		Если Не Файл.ЭтоКаталог() Тогда
			Продолжить;
		КонецЕсли;

		ИмяПакета = Файл.Имя;

		Если НайденныеПакеты[ИмяПакета] = Неопределено Тогда
			НайденныеПакеты.Вставить(ИмяПакета, Файл.ПолноеИмя);
		КонецЕсли;

	КонецЦикла;
	
КонецПроцедуры

Функция ПрочитатьПараметрыКонфигурации(КаталогБиблиотек)
	
	ПараметрыКонфигурации = Новый Соответствие();	

	ИменаПараметров = Новый МножествоСоответствие();
	ИменаПараметров.Добавить(_ИмяПараметраКаталогаБиблиотек);
	ИменаПараметров.Добавить(_ИмяПараметраДополнительныхКаталоговБиблиотек);

	ПрочитатьПараметрыКонфигурацииИзКаталога(КаталогПрограммы(), ПараметрыКонфигурации, ИменаПараметров);

	Если Не ЗначениеЗаполнено(КаталогБиблиотек) Тогда
		ПрочитатьПараметрыКонфигурацииИзКаталога(СтартовыйСценарий().Каталог, ПараметрыКонфигурации, ИменаПараметров);
	КонецЕсли;

	ПрочитатьПараметрыКонфигурацииИзПеременныхСреды(ПараметрыКонфигурации, ИменаПараметров);

	Возврат ПараметрыКонфигурации;

КонецФункции

Процедура ПрочитатьПараметрыКонфигурацииИзКаталога(Каталог, ПараметрыКонфигурации, ИменаПараметров)

	ФайлПараметров = ОбъединитьПути(Каталог, "oscript.cfg");

	Если Не ФС.ФайлСуществует(ФайлПараметров) Тогда
		Возврат;
	КонецЕсли;
		
	МенеджерПараметров = Новый МенеджерПараметров();
	МенеджерПараметров.ДобавитьПровайдерПараметров(Новый ПровайдерПараметровCFG());

	НастройкаФайловогоПровайдера = МенеджерПараметров.НастройкаПоискаФайла();
	НастройкаФайловогоПровайдера.УстановитьФайлПараметров(ФайлПараметров);
	МенеджерПараметров.Прочитать();

	Для Каждого Строка Из МенеджерПараметров.ПрочитанныеПараметры() Цикл
		Если ИменаПараметров.Содержит(Строка.Ключ) И ЗначениеЗаполнено(Строка.Значение) Тогда
			ПараметрыКонфигурации[Строка.Ключ] = ПолучитьАбсолютныйПуть(Строка.Значение, Каталог);
		КонецЕсли;
	КонецЦикла;

КонецПроцедуры

Процедура ПрочитатьПараметрыКонфигурацииИзПеременныхСреды(ПараметрыКонфигурации, ИменаПараметров)

	Каталог = СтартовыйСценарий().Каталог;
	
	КонфигурацияСтрокой = ПолучитьПеременнуюСреды(_ИмяПеременнойСредыКонфигурации);
	Если Не ЗначениеЗаполнено(КонфигурацияСтрокой) Тогда
		Возврат;
	КонецЕсли;
	
	КлючиИЗначения = СтрРазделить(КонфигурацияСтрокой, ";");

	Для Каждого КлючИЗначение Из КлючиИЗначения Цикл
		ПозицияРавенства = СтрНайти(КлючИЗначение, "=");
		Если ПозицияРавенства > 0 Тогда
			Имя = СокрЛП(Сред(КлючИЗначение, 1, ПозицияРавенства - 1));
			Значение = СокрЛП(Сред(КлючИЗначение, ПозицияРавенства + 1));

			Если ИменаПараметров.Содержит(Имя) И ЗначениеЗаполнено(Значение) Тогда
				ПараметрыКонфигурации[Имя] = ПолучитьАбсолютныйПуть(Значение, Каталог);
			КонецЕсли;
		КонецЕсли;
	КонецЦикла;

КонецПроцедуры

Функция ПолучитьАбсолютныйПуть(Путь, Каталог)

	Если РаботаСПутямиФайловойСистемы.ЭтоАбсолютныйПуть(Путь) Тогда
		Возврат Путь;
	КонецЕсли;

	Возврат ФС.НормализоватьПуть(ОбъединитьПути(Каталог, Путь));
	
КонецФункции

#КонецОбласти