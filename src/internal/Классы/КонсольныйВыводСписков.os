#Использовать coloratos

Перем _Список; // Массив
Перем _Настройки; // Структура

Процедура ПриСозданииОбъекта()

	_Настройки = НовыеНастройки();

КонецПроцедуры

#Область ПрограммныйИнтерфейс

Процедура УстановитьСписок(Список) Экспорт
	
	_Список = Список;

КонецПроцедуры

Процедура Вывести() Экспорт

	Если _Список.Количество() = 0 Тогда
		Возврат;
	КонецЕсли;

	ВывестиСтроки();

КонецПроцедуры

Функция ПолучитьНастройки() Экспорт

	Настройки = СкопироватьСтруктуру(_Настройки);

	Возврат Настройки;

КонецФункции

Процедура УстановитьНастройки(Настройки) Экспорт

	ЗаполнитьЗначенияСвойств(_Настройки, Настройки);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Процедура ВывестиСтроки()

	КоличествоЭлементов = _Список.Количество();
	НомерЭлемента = 0;

	Для Каждого Значение Из _Список Цикл

		НомерЭлемента = НомерЭлемента + 1;

		Если _Настройки.ОтступСлева > 0 Тогда
			Консоль.Вывести(ПомощникКонсольногоВывода.ФиксированнаяСтрока(" ", _Настройки.ОтступСлева));
		КонецЕсли;

		Если _Настройки.ИспользоватьПсевдографику Тогда
			Если НомерЭлемента = КоличествоЭлементов Тогда
				Консоль.Вывести("└─ ");
			Иначе
				Консоль.Вывести("├─ ");
			КонецЕсли;
		КонецЕсли;

		ЦветнойВывод.ВывестиСтроку(Значение);

	КонецЦикла;
	
КонецПроцедуры

Функция СкопироватьСтруктуру(Значение)
	Возврат Новый Структура(Новый ФиксированнаяСтруктура(Значение));
КонецФункции

Функция НовыеНастройки()
	
	Настройки = Новый Структура();
	Настройки.Вставить("ОтступСлева", 0);
	Настройки.Вставить("ИспользоватьПсевдографику", Истина);

	Возврат Настройки;

КонецФункции

#КонецОбласти