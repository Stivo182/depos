#Использовать coloratos

Перем _Таблица; // ТаблицаЗначений
Перем _Настройки; // Структура

Процедура ПриСозданииОбъекта()

	_Настройки = НовыеНастройки();

КонецПроцедуры

#Область ПрограммныйИнтерфейс

Процедура УстановитьТаблицу(Таблица) Экспорт

	_Таблица = Таблица;

	ПодготовитьНастройкиКолонок();

КонецПроцедуры

Процедура Вывести() Экспорт

	Если _Таблица.Количество() = 0 Тогда
		Возврат;
	КонецЕсли;

	ШиринаКолонок = МаксимальнаяШиринаКолонок(_Таблица);

	ВывестиЗаголовки(ШиринаКолонок);
	ВывестиСтроки(ШиринаКолонок);

КонецПроцедуры

Функция ПолучитьНастройки() Экспорт

	Настройки = СкопироватьСтруктуру(_Настройки);
	Настройки.Колонки = СкопироватьСтруктуру(Настройки.Колонки);

	Возврат Настройки;

КонецФункции

Процедура УстановитьНастройки(Настройки) Экспорт

	ЗаполнитьЗначенияСвойств(_Настройки, Настройки, "ОтступСлева, ВыводитьЗаголовки");

	_Настройки.Колонки.Очистить();

	Для Каждого Строка Из Настройки.Колонки Цикл

		НастройкиКолонки = НовыеНастройкиКолонки();
		ЗаполнитьЗначенияСвойств(НастройкиКолонки, Строка.Значение);

		_Настройки.Колонки.Вставить(Строка.Ключ, НастройкиКолонки);

	КонецЦикла;
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Процедура ВывестиЗаголовки(ШиринаКолонок)

	Если Не _Настройки.ВыводитьЗаголовки Тогда
		Возврат;
	КонецЕсли;

	ВывестиОтступСлеваОтТаблицы();

	Для Каждого Колонка Из _Таблица.Колонки Цикл

		ИмяКолонки = Колонка.Имя;

		Если Не _Настройки.Колонки.Свойство(ИмяКолонки) Тогда
			Продолжить;
		КонецЕсли;

		НастройкиКолонки = _Настройки.Колонки[ИмяКолонки];
		Значение = НастройкиКолонки.Заголовок;
		
		ВывестиПоле(Значение, ИмяКолонки, НастройкиКолонки, ШиринаКолонок);

	КонецЦикла;

	ВывестиЛинию(ШиринаКолонок);

КонецПроцедуры

Процедура ВывестиЛинию(ШиринаКолонок)

	ВывестиОтступСлеваОтТаблицы();
		
	Для Каждого Колонка Из _Таблица.Колонки Цикл

		ИмяКолонки = Колонка.Имя;

		Если Не _Настройки.Колонки.Свойство(ИмяКолонки) Тогда
			Продолжить;
		КонецЕсли;

		НастройкиКолонки = _Настройки.Колонки[ИмяКолонки];
		
		ВывестиПоле("", ИмяКолонки, НастройкиКолонки, ШиринаКолонок, "-");

	КонецЦикла;

КонецПроцедуры

Процедура ВывестиСтроки(ШиринаКолонок)

	Для Каждого СтрокаТаблицы Из _Таблица Цикл
	
		ВывестиОтступСлеваОтТаблицы();

		Для Каждого Колонка Из _Таблица.Колонки Цикл

			ИмяКолонки = Колонка.Имя;

			Если Не _Настройки.Колонки.Свойство(ИмяКолонки) Тогда
				Продолжить;
			КонецЕсли;

			НастройкиКолонки = _Настройки.Колонки[ИмяКолонки];
			Значение = СтрокаТаблицы[ИмяКолонки];
			
			ВывестиПоле(Значение, ИмяКолонки, НастройкиКолонки, ШиринаКолонок);

		КонецЦикла;

	КонецЦикла;

КонецПроцедуры

Процедура ВывестиПоле(Значение, ИмяКолонки, НастройкиКолонки, ШиринаКолонок, ЗаполняемыйСимвол = " ")

	ПоследняяКолонка = _Таблица.Колонки[_Таблица.Колонки.Количество() - 1];
	ЭтоПоследняяКолонка = ПоследняяКолонка = _Таблица.Колонки[ИмяКолонки];
	ОтступСлева = Мин(НастройкиКолонки.Отступ, НастройкиКолонки.ОтступСлева);
	ОтступСправа = Мин(НастройкиКолонки.Отступ, НастройкиКолонки.ОтступСправа);
	
	Ширина = ?(НастройкиКолонки.Ширина <> 0, НастройкиКолонки.Ширина, ШиринаКолонок[ИмяКолонки]);

	Текст = ПомощникКонсольногоВывода.ФиксированнаяСтрока(Значение, 
		Ширина, 
		НастройкиКолонки.ПрижатьВПраво,
		ЗаполняемыйСимвол
	);

	ВывестиОтступ(ОтступСлева);

	Если ЭтоПоследняяКолонка Тогда
		ЦветнойВывод.ВывестиСтроку(Текст);
	Иначе
		ЦветнойВывод.Вывести(Текст);
		ВывестиОтступ(ОтступСправа);
	КонецЕсли;

КонецПроцедуры

Процедура ВывестиОтступСлеваОтТаблицы()
	ВывестиОтступ(_Настройки.ОтступСлева);
КонецПроцедуры

Процедура ВывестиОтступ(Отступ, ЗаполняемыйСимвол = " ")
	
	Если Отступ > 0 Тогда
		Консоль.Вывести(ПомощникКонсольногоВывода.ФиксированнаяСтрока(ЗаполняемыйСимвол, Отступ));
	КонецЕсли;

КонецПроцедуры

Процедура ПодготовитьНастройкиКолонок()

	_Настройки.Колонки.Очистить();

	Для Каждого Колонка Из _Таблица.Колонки Цикл

		НастройкиКолонки = НовыеНастройкиКолонки();
		НастройкиКолонки.Заголовок = Колонка.Заголовок;

		_Настройки.Колонки.Вставить(Колонка.Имя, НастройкиКолонки);

	КонецЦикла;

КонецПроцедуры

Функция МаксимальнаяШиринаКолонок(Таблица)

	Результат = Новый Структура();
	
	Для Каждого Колонка Из Таблица.Колонки Цикл

		ИмяКолонки = Колонка.Имя;

		Если Не _Настройки.Колонки.Свойство(ИмяКолонки) Тогда
			Продолжить;
		КонецЕсли;
			
		НастройкиКолонки = _Настройки.Колонки[ИмяКолонки];
		Ширина = 0;

		Если _Настройки.ВыводитьЗаголовки Тогда
			Ширина = СтрДлина(НастройкиКолонки.Заголовок);
		КонецЕсли;

		Результат.Вставить(ИмяКолонки, Ширина);

	КонецЦикла;

	Для Каждого Строка Из Таблица Цикл
		Для Каждого Колонка Из Таблица.Колонки Цикл
			Если _Настройки.Колонки.Свойство(ИмяКолонки) Тогда
				ДлинаЗначения = ПомощникКонсольногоВывода.ДлинаСтрокиБезЦвета(Строка[Колонка.Имя]);
				Результат[Колонка.Имя] = Макс(Результат[Колонка.Имя], ДлинаЗначения);
			КонецЕсли;		
		КонецЦикла;
	КонецЦикла;

	Возврат Результат;

КонецФункции

Функция СкопироватьСтруктуру(Значение)
	Возврат Новый Структура(Новый ФиксированнаяСтруктура(Значение));
КонецФункции

Функция НовыеНастройки()
	
	Настройки = Новый Структура();
	Настройки.Вставить("ОтступСлева", 0);
	Настройки.Вставить("ВыводитьЗаголовки", Истина);
	Настройки.Вставить("Колонки", Новый Структура());

	Возврат Настройки;

КонецФункции

Функция НовыеНастройкиКолонки()
	
	Настройки = Новый Структура();
	Настройки.Вставить("ПрижатьВПраво", Ложь);
	Настройки.Вставить("Ширина", 0);
	Настройки.Вставить("Отступ", 1);
	Настройки.Вставить("ОтступСлева", 1);
	Настройки.Вставить("ОтступСправа", 1);
	Настройки.Вставить("Заголовок", "");

	Возврат Настройки;

КонецФункции

#КонецОбласти