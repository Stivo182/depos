#Использовать jason
#Использовать "../../dto"

// Записывает изменения зависимостей в JSON-файл.
//
// Параметры:
//   ИзмененияЗависимостей - ТаблицаЗначений - см. ЗависимостиПакета.ВычислитьИзменения
//   ПолноеИмяФайла - Строка - Полный путь к JSON-файлу, в который производится запись.
Процедура Записать(ИзмененияЗависимостей, ПолноеИмяФайла) Экспорт
	
	Сериализатор = Новый СериализаторJson();
	СтрокаJson = Сериализатор.Сериализовать(ТаблицуВДто(ИзмененияЗависимостей));

	ЗаписьТекста = Новый ЗаписьТекста(ПолноеИмяФайла, "UTF-8");
	ЗаписьТекста.Записать(СтрокаJson);
	ЗаписьТекста.Закрыть();

КонецПроцедуры

Функция ТаблицуВДто(ИзмененияЗависимостей)

	Результат = Новый Массив();
	Для Каждого СтрокаТаблицы Из ИзмененияЗависимостей Цикл
		
		ЗаписьДто = Новый ИзмененнаяЗависимостьДто;
		ЗаписьДто.ИмяПакета = СтрокаТаблицы.ИмяПакета;
		ЗаписьДто.ДляРазработки = СтрокаТаблицы.ДляРазработки;
		ЗаписьДто.МинимальнаяВерсияДо = СтрокаТаблицы.МинимальнаяВерсияДо;
		ЗаписьДто.МинимальнаяВерсияПосле = СтрокаТаблицы.МинимальнаяВерсияПосле;
		ЗаписьДто.МаксимальнаяВерсия = СтрокаТаблицы.МаксимальнаяВерсия;

		Результат.Добавить(ЗаписьДто);

	КонецЦикла;

	Возврат Результат;
	
КонецФункции