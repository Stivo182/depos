
Функция ЭтоАбсолютныйПуть(Знач Путь) Экспорт
	Если ЭтоWindows() Тогда
		Возврат ЭтоАбсолютныйПутьWindows(Путь);
	Иначе
		Возврат ЭтоАбсолютныйПутьUnix(Путь);
	КонецЕсли;
КонецФункции

Функция ЭтоАбсолютныйПутьUnix(Знач Путь) Экспорт
	Путь = СокрЛП(Путь);
	Возврат Лев(Путь, 1) = "/";
КонецФункции

Функция ЭтоАбсолютныйПутьWindows(Знач Путь) Экспорт
	
	Путь = СокрЛП(Путь);
	ПервыйСимвол = Лев(Путь, 1);
	
	Если ПервыйСимвол = "\" Или ПервыйСимвол = "/" Тогда
		Возврат Истина;
	КонецЕсли;

	// C:\ или C:/
	Если СтрДлина(Путь) >= 3 Тогда
		ВторойСимвол = Сред(Путь, 2, 1);
		ТретийСимвол = Сред(Путь, 3, 1);
		
		Если ЭтоДопустимаяБукваДискаWindows(ПервыйСимвол) 
			И ВторойСимвол = ":" 
			И (ТретийСимвол = "\" Или ТретийСимвол = "/") Тогда
			Возврат Истина;
		КонецЕсли;
	КонецЕсли;

	Возврат Ложь;

КонецФункции

Функция ЭтоДопустимаяБукваДискаWindows(Символ)

	Код = КодСимвола(Символ);

	Возврат (Код >= КодСимвола("A") И Код <= КодСимвола("Z"))
		ИЛИ (Код >= КодСимвола("a") И Код <= КодСимвола("z"));

КонецФункции

Функция ЭтоWindows()
	Платформа = Новый СистемнаяИнформация().ТипПлатформы;
	Возврат Платформа = ТипПлатформы.Windows_x86 Или Платформа = ТипПлатформы.Windows_x86_64;
КонецФункции