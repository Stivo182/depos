#Использовать coloratos

#Область Опции

&Опция(
	Имя = "f filter",
	Описание = "Фильтр пакетов по именам (через запятую или пробел), шаблону (*, ?) или регулярному выражению (например, /^autumn-.*$/)"
)
&ТСтрока
Перем _ФильтрПакетов; // Строка

&Опция(Имя = "t target", Описание = "Тип целевой версии")
&ПоУмолчанию("Последняя")
&ТПеречисление
&Перечисление(
	Имя = "latest",
	Значение = "Последняя",
	Описание = "Последняя доступная версия пакета"
)
&Перечисление(
	Имя = "minor",
	Значение = "Минорная",
	Описание = "Последняя минорная или патч-версия в пределах основной версии"
)
&Перечисление(
	Имя = "patch",
	Значение = "Патч",
	Описание = "Последняя патч-версия в пределах минорной версии"
)
Перем _ТипВерсии; // Строка, Неопределено

&Опция(Имя = "packagedef", Описание = "Путь к файлу packagedef (по умолчанию ищется в текущей директории)")
&ТСтрока
Перем _ФайлМанифеста; // Строка, Неопределено

&Опция(Имя = "backup", Описание = "Создать резервную копию файла packagedef перед изменением")
&Флаг
&ПоУмолчанию(Ложь)
Перем _СохранятьКопию; // Булево

#КонецОбласти

#Область ОписаниеПеременных

&ЛогDepos
Перем _Лог; // Лог

&Пластилин("Поделка")
Перем _Поделка; // Поделка

&Пластилин("ЛокаторМанифеста")
Перем _ЛокаторМанифеста; // ЛокаторМанифеста

&Пластилин("ВизуализаторЗависимостей")
Перем _ВизуализаторЗависимостей; // ВизуализаторЗависимостей

#КонецОбласти

&КомандаПриложения(Имя = "upgrade", Описание = "Обновление зависимостей")
Процедура ПриСозданииОбъекта()
КонецПроцедуры

&ВыполнениеКоманды
Процедура ЗапуститьБезопасно() Экспорт

	Попытка
		Запустить();
	Исключение
		_Лог.Отладка(ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()));
		_Лог.Ошибка(КраткоеПредставлениеОшибки(ИнформацияОбОшибке()));
		ЗавершитьРаботу(1);
	КонецПопытки;

КонецПроцедуры

Процедура Запустить()
		
	ОпределительВерсийЗависимостей = _Поделка.НайтиЖелудь("ОпределительВерсийЗависимостей");
	ОбновляторЗависимостей = _Поделка.НайтиЖелудь("ОбновляторЗависимостей");
	
	ФайлМанифеста = ОпределитьФайлМанифеста();

	Консоль.ВывестиСтроку(СтрШаблон("Обновление зависимостей в %1" + Символы.ПС, ФайлМанифеста));

	Зависимости = ОпределительВерсийЗависимостей
		.ИзФайла(ФайлМанифеста)
		.ТолькоУстаревшие()
		.ФильтроватьПоИмени(_ФильтрПакетов)
		.УстановитьТипВерсии(_ТипВерсии)
		.Получить();

	Если Зависимости.Количество() = 0 Тогда
		ЦветнойВывод.ВывестиСтроку("Не найдены зависимости для обновления", "ТемноЖелтый");
		Возврат;
	КонецЕсли;

	_ВизуализаторЗависимостей.Вывести(Зависимости);

	Зависимости.Колонки.Удалить("МинимальнаяВерсия");
	Зависимости.Колонки.ЦелеваяВерсия.Имя = "МинимальнаяВерсия";

	ОбновляторЗависимостей
		.ДляФайла(ФайлМанифеста)
		.ЗависитОтПакетов(Зависимости)
		.СохранятьКопию(_СохранятьКопию)
		.Обновить();

	Консоль.ВывестиСтроку(" ");
	Консоль.ВывестиСтроку("Выполните opm install для установки новых версий пакетов.");

КонецПроцедуры

Функция ОпределитьФайлМанифеста() Экспорт
	
	Если ЗначениеЗаполнено(_ФайлМанифеста) Тогда
		Возврат _ФайлМанифеста;
	КонецЕсли;
	
	ФайлМанифеста = _ЛокаторМанифеста.НайтиТекущемКаталоге();
	Если Не ЗначениеЗаполнено(ФайлМанифеста) Тогда
		ВызватьИсключение "Не найден файл манифеста в текущем каталоге";
	КонецЕсли;
	
	Возврат ФайлМанифеста;
	
КонецФункции