#Использовать coloratos
#Использовать "../../core"
#Использовать "../../internal"

#Область Опции

&Опция(Имя = "p packagedef", Описание = "Путь к файлу packagedef или его каталогу (по умолчанию - текущая директория)")
&ТСтрока
Перем _ФайлМанифеста; // Строка, Неопределено

&Опция(Имя = "d deprecated", Описание = "Показывать только устаревшие пакеты")
&Флаг
&ПоУмолчанию(Ложь)
Перем _ТолькоУстаревшие; // Булево

&Опция(
	Имя = "f filter",
	Описание = "Фильтр пакетов по именам (через запятую или пробел), шаблону (*, ?) или регулярному выражению (например, /^autumn-.*$/)"
)
&ТСтрока
Перем _ФильтрПоИмени; // Строка

&Опция(Имя = "t target", Описание = "Тип целевой версии")
&ПоУмолчанию("Последняя")
&ТПеречисление
&Перечисление(
	Имя = "latest",
	Значение = "Последняя",
	Описание = "Последняя доступная версия пакета"
)
&Перечисление(
	Имя = "minor",
	Значение = "Минорная",
	Описание = "Последняя минорная или патч-версия в пределах основной версии"
)
&Перечисление(
	Имя = "patch",
	Значение = "Патч",
	Описание = "Последняя патч-версия в пределах минорной версии"
)
Перем _ТипВерсии; // Строка, Неопределено

#КонецОбласти

#Область ОписаниеПеременных

&ЛогDepos
Перем _Лог; // Лог

&Пластилин("Поделка")
Перем _Поделка; // Поделка

&Пластилин("НастройкиПриложения")
Перем _НастройкиПриложения; // НастройкиПриложения

&Пластилин("ЛокаторМанифеста")
Перем _ЛокаторМанифеста; // ЛокаторМанифеста

&Пластилин("КонсольныйВыводЗависимостей")
Перем _КонсольныйВыводЗависимостей; // КонсольныйВыводЗависимостей

#КонецОбласти

&КомандаПриложения(Имя = "check", Описание = "Проверка зависимостей")
Процедура ПриСозданииОбъекта()
КонецПроцедуры

&ВыполнениеКоманды
Процедура ЗапуститьБезопасно() Экспорт

	Попытка
		Запустить();
	Исключение
		_Лог.Отладка(ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()));
		_Лог.Ошибка(КраткоеПредставлениеОшибки(ИнформацияОбОшибке()));
		
		Если _НастройкиПриложения.РежимОтладки Тогда
			Консоль.ВывестиСтроку(КраткоеПредставлениеОшибки(ИнформацияОбОшибке()));
		Иначе
			ЗавершитьРаботу(1);
		КонецЕсли;
	КонецПопытки;

КонецПроцедуры

Процедура Запустить()

	ОпределительВерсийЗависимостей = _Поделка.НайтиЖелудь("ОпределительВерсийЗависимостей");	
	ФайлМанифеста = _ЛокаторМанифеста.Определить(_ФайлМанифеста);

	Консоль.ВывестиСтроку(СтрШаблон("Проверка зависимостей: %1" + Символы.ПС, ФайлМанифеста));

	ИсходныеЗависимости = Новый ЗависимостиПакета()
		.ПрочитатьИзМанифеста(ФайлМанифеста)
		.ФильтроватьПоИмени(_ФильтрПоИмени);

	ЦелевыеЗависимости = ОпределительВерсийЗависимостей
		.ТолькоУстаревшие(_ТолькоУстаревшие)
		.НайтиВерсии(ИсходныеЗависимости, _ТипВерсии);

	Если ЦелевыеЗависимости.Пустой() Тогда
		ЦветнойВывод.ВывестиСтроку("Не найдены зависимости по заданным условиям", "ТемноЖелтый");
		Возврат;
	КонецЕсли;

	_КонсольныйВыводЗависимостей.ВывестиКакПроверенные(ЦелевыеЗависимости, ИсходныеЗависимости);
	ВывестиКоличествоОбновлений(КоличествоОбновлений(ЦелевыеЗависимости, ИсходныеЗависимости));

КонецПроцедуры

Процедура ВывестиКоличествоОбновлений(Количество)

	Консоль.ВывестиСтроку(" ");

	Если Количество > 0 Тогда
		Консоль.ВывестиСтроку("Найдено обновлений: " + Количество);
	Иначе
		ЦветнойВывод.ВывестиСтроку(СтрШаблон("%1 Все зависимости актуальны!", НаборСимволов.ОК), "Зеленый");
	КонецЕсли;
	
КонецПроцедуры

Функция КоличествоОбновлений(ЦелевыеЗависимости, ИсходныеЗависимости)
		
	Количество = 0;
	ТаблицаИсходные = ИсходныеЗависимости.ВТаблицу();

	Для Каждого СтрокаЦелевая Из ЦелевыеЗависимости.ВТаблицу() Цикл
		
		СтрокаИсходная = ТаблицаИсходные.Найти(СтрокаЦелевая.ИмяПакета, "ИмяПакета");
		ИсходнаяВерсия = ?(СтрокаИсходная = Неопределено, "", СтрокаИсходная.МинимальнаяВерсия);

		Если СтрокаЦелевая.МинимальнаяВерсия <> ИсходнаяВерсия Тогда
			Количество = Количество + 1;
		КонецЕсли;

	КонецЦикла;

	Возврат Количество;

КонецФункции