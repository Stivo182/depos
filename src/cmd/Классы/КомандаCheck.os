#Использовать coloratos
#Использовать fluent

#Область Опции

&Опция(Имя = "d deprecated", Описание = "Показывать только устаревшие пакеты")
&Флаг
&ПоУмолчанию(Ложь)
Перем _ТолькоУстаревшие; // Булево

&Опция(
	Имя = "f filter",
	Описание = "Фильтр пакетов по именам (через запятую или пробел), шаблону (*, ?) или регулярному выражению (например, /^autumn-.*$/)"
)
&ТСтрока
Перем _ФильтрПакетов; // Строка

&Опция(Имя = "t target", Описание = "Тип целевой версии")
&ПоУмолчанию("Последняя")
&ТПеречисление
&Перечисление(
	Имя = "latest",
	Значение = "Последняя",
	Описание = "Последняя доступная версия пакета"
)
&Перечисление(
	Имя = "minor",
	Значение = "Минорная",
	Описание = "Последняя минорная или патч-версия в пределах основной версии"
)
&Перечисление(
	Имя = "patch",
	Значение = "Патч",
	Описание = "Последняя патч-версия в пределах минорной версии"
)
Перем _ТипВерсии; // Строка, Неопределено

&Опция(Имя = "packagedef", Описание = "Путь к файлу packagedef (по умолчанию ищется в текущей директории)")
&ТСтрока
Перем _ФайлМанифеста; // Строка, Неопределено

#КонецОбласти

#Область ОписаниеПеременных

&ЛогDepos
Перем _Лог; // Лог

&Пластилин("Поделка")
Перем _Поделка; // Поделка

&Пластилин("НастройкиПриложения")
Перем _НастройкиПриложения; // НастройкиПриложения

&Пластилин("ЛокаторМанифеста")
Перем _ЛокаторМанифеста; // ЛокаторМанифеста

&Пластилин("ВизуализаторЗависимостей")
Перем _ВизуализаторЗависимостей; // ВизуализаторЗависимостей

#КонецОбласти

&КомандаПриложения(Имя = "check", Описание = "Проверка зависимостей")
Процедура ПриСозданииОбъекта()
КонецПроцедуры

&ВыполнениеКоманды
Процедура ЗапуститьБезопасно() Экспорт

	Попытка
		Запустить();
	Исключение
		_Лог.Отладка(ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()));
		_Лог.Ошибка(КраткоеПредставлениеОшибки(ИнформацияОбОшибке()));

		Если Не _НастройкиПриложения.РежимОтладки Тогда
			ЗавершитьРаботу(1);
		КонецЕсли;
	КонецПопытки;

КонецПроцедуры

Процедура Запустить()

	ОпределительВерсийЗависимостей = _Поделка.НайтиЖелудь("ОпределительВерсийЗависимостей");
	
	ФайлМанифеста = ОпределитьФайлМанифеста();

	Консоль.ВывестиСтроку(СтрШаблон("Проверка зависимостей в %1" + Символы.ПС, ФайлМанифеста));

	Зависимости = ОпределительВерсийЗависимостей
		.ИзФайла(ФайлМанифеста)
		.ТолькоУстаревшие(_ТолькоУстаревшие)
		.ФильтроватьПоИмени(_ФильтрПакетов)
		.УстановитьТипВерсии(_ТипВерсии)
		.Получить();

	Если Зависимости.Количество() = 0 Тогда
		ЦветнойВывод.ВывестиСтроку("Не найдены зависимости по заданным условиям", "ТемноЖелтый");
		Возврат;
	КонецЕсли;

	_ВизуализаторЗависимостей.ВывестиКакПроверенные(Зависимости);
	ВывестиКоличествоОбновлений(Зависимости);

КонецПроцедуры

Функция ОпределитьФайлМанифеста() Экспорт
	
	Если ЗначениеЗаполнено(_ФайлМанифеста) Тогда
		Возврат _ФайлМанифеста;
	КонецЕсли;
	
	ФайлМанифеста = _ЛокаторМанифеста.НайтиТекущемКаталоге();
	Если Не ЗначениеЗаполнено(ФайлМанифеста) Тогда
		ВызватьИсключение "Не найден файл манифеста в текущем каталоге";
	КонецЕсли;
	
	Возврат ФайлМанифеста;
	
КонецФункции

Процедура ВывестиКоличествоОбновлений(Зависимости)

	Количество = ПроцессорыКоллекций.ИзКоллекции(Зависимости)
		.Фильтровать("Стр -> ЗначениеЗаполнено(Стр.МинимальнаяВерсия) И Стр.МинимальнаяВерсия <> Стр.ЦелеваяВерсия")
		.Количество();

	Консоль.ВывестиСтроку(" ");
	Консоль.ВывестиСтроку("Найдено обновлений: " + Количество);
	
КонецПроцедуры