#Использовать coloratos

#Область Опции

&Опция(Имя = "packagedef", Описание = "Путь к packagedef или его каталогу (по умолчанию - текущая директория)")
&ТСтрока
Перем _ФайлМанифеста; // Строка

&Опция(Имя = "src-dirs", Описание = "Каталоги исходников (через запятую). По умолчанию: src")
&ТСтрока
Перем _КаталогиИсходников; // Строка

&Опция(Имя = "dev-dirs", Описание = "Каталоги служебных исходников (через запятую). По умолчанию: tests и tasks")
&ТСтрока
Перем _КаталогиСлужебныхИсходников; // Строка

&Опция(Имя = "ignore-dev", Описание = "Игнорировать служебные исходники")
&Флаг
&ПоУмолчанию(Ложь)
Перем _ИгнорироватьСлужебныеИсходники; // Булево

&Опция(Имя = "strict", Описание = "Ненулевой код возврата при любых найденных проблемах")
&Флаг
&ПоУмолчанию(Ложь)
Перем _СтрогийРежим; // Булево

#КонецОбласти

#Область ОписаниеПеременных

&ЛогDepos
Перем _Лог; // Лог

&Пластилин("Поделка")
Перем _Поделка; // Поделка

&Пластилин("НастройкиПриложения")
Перем _НастройкиПриложения; // НастройкиПриложения

&Пластилин("ПомощникМанифеста")
Перем _ПомощникМанифеста; // ПомощникМанифеста

&Пластилин("КонсольныйВыводРезультатовАнализа")
Перем _КонсольныйВыводРезультатовАнализа; // КонсольныйВыводРезультатовАнализа

#КонецОбласти

&КомандаПриложения(Имя = "doctor", Описание = "Диагностика зависимостей")
Процедура ПриСозданииОбъекта()
КонецПроцедуры

&ВыполнениеКоманды
Процедура ЗапуститьБезопасно() Экспорт

	Попытка
		Запустить();
	Исключение
		_Лог.Отладка(ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()));
		_Лог.Ошибка(КраткоеПредставлениеОшибки(ИнформацияОбОшибке()));

		Если Не _НастройкиПриложения.РежимОтладки Тогда
			ЗавершитьРаботу(1);
		КонецЕсли;
	КонецПопытки;

КонецПроцедуры

Процедура Запустить()

	АнализаторЗависимостей = _Поделка.НайтиЖелудь("АнализаторЗависимостей");	
	ФайлМанифеста = _ПомощникМанифеста.ОпределитьФайлМанифеста(_ФайлМанифеста);

	Консоль.ВывестиСтроку(СтрШаблон("Диагностика зависимостей: %1" + Символы.ПС, ФайлМанифеста));

	АнализаторЗависимостей
		.ИзФайла(ФайлМанифеста)
		.УстановитьИсходники(ПолучитьКаталогиИсходников());

	Если Не _ИгнорироватьСлужебныеИсходники Тогда
		АнализаторЗависимостей.УстановитьСлужебныеИсходники(ПолучитьКаталогиСлужебныхИсходников());
	Иначе
		АнализаторЗависимостей.ИсключитьЗависимостиДляРазработки();
	КонецЕсли;

	РезультатАнализа = АнализаторЗависимостей.НайтиОшибки();

	_КонсольныйВыводРезультатовАнализа.Вывести(РезультатАнализа);

	Если Не _НастройкиПриложения.РежимОтладки Тогда
		ЗавершитьРаботу(ОпределитьКодВозврата(РезультатАнализа));
	КонецЕсли;

КонецПроцедуры

Функция ПолучитьКаталогиИсходников()
	Возврат ?(ЗначениеЗаполнено(_КаталогиИсходников), _КаталогиИсходников, _НастройкиПриложения.КаталогиИсходников);
КонецФункции

Функция ПолучитьКаталогиСлужебныхИсходников()
	Возврат ?(ЗначениеЗаполнено(_КаталогиСлужебныхИсходников),
				_КаталогиСлужебныхИсходников,
				_НастройкиПриложения.КаталогиСлужебныхИсходников
			);
КонецФункции

Функция ОпределитьКодВозврата(РезультатАнализа)

	Если _СтрогийРежим И РезультатАнализа.ЕстьПроблемы() Тогда
		Возврат 1;
	КонецЕсли;

	Возврат 0;

КонецФункции